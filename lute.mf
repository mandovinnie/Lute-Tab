
%
%  a new font 
%
%
%
mid_bar#:=7/13cap#;
define_good_y_pixels(mid_bar);
%v_bar#:=3/10em#;
v_bar#:=1/10em#;
define_good_x_pixels(v_bar);
flag_top#:=3em#;
define_good_y_pixels(flag_top);
%
pickup pencircle scaled 0.5wid;
tiny_circle := savepen;
pickup pencircle xscaled 1.2wid yscaled .3 wid rotated 50;
lute_pen := savepen;
pickup pencircle xscaled 1.4wid yscaled .3 wid rotated 50;
bigger_lute_pen := savepen;
pickup pencircle xscaled 1.5wid yscaled .1 rotated 85;
flag_pen := savepen;
pickup pencircle xscaled .3wid yscaled .1 rotated 50;
stem_pen := savepen;
%
def do_v_bar = 
   pickup lute_pen;x3 = x4 = v_bar;top y3 = h;top y4 = 0;
   draw z3..z4;
enddef;
%
def do_box =
beginchar (0, em#, 1.0cap#, .3cap#); "em quad";
  pickup pencircle xscaled .3wid yscaled .3wid;
  x1=x4=0w;x2=x3=w;
  y1=y2=0h;y3=y4=h;
  x5=x1;y5=-d;
  x6=x2;y6=-d;
  draw z1..z2;draw z2..z3; draw z3..z4; draw z4..z1;
  draw z5..z6;
endchar;enddef;
%
def do_v =  
beginchar(8, .7em#, .5cap#, 0cap#); "a little v thingy";
  pickup pencircle xscaled .9wid yscaled .3 wid;
  lft x1=0w;top y1=h;
  x2=.5w;bot y2=0;
  rt x3=w;top y3=h;
  draw z1..z2&z2..z3;
endchar;enddef;
%%
def do_vv =  
beginchar(9, .7em#, .5cap#, 0cap#); "a little v thingy";
  pickup pencircle xscaled .9wid yscaled .3 wid;
  lft x1=0w;bot y1=0;
  x2=.5w;top y2=h;
  rt x3=w;top y3=y1;
  draw z1..z2&z2..z3;
endchar;enddef;
%
%
def do_a_flag(suffix $)(expr the_top) =
 pickup flag_pen;
 x$1=v_bar;x$2=w;top y$1=the_top;y$2=y$1 - 12/30h;
 draw z$1{dir 250}..tension 1.4..{dir 250}z$2;
enddef;
%
def do_a_big_flag(suffix $)(expr the_top) =
 pickup pencircle xscaled 1.7 wid yscaled .3 rotated 85;
 x$1=v_bar;x$2=w;
 top y$1=the_top;y$2=y$1 - 19/30h;
 x25=1/2[x$1,x$2];
 y25=1/2[y$1,y$2];
 draw z$1{dir 250}..tension 1.4..z25..{dir 250}z$2;
enddef;
%
def do_a_med_flag(suffix $)(expr the_top) =
 pickup pencircle xscaled 1.7 wid yscaled .3 rotated 85;
 x$1=v_bar;x$2=1.0w;
 top y$1=the_top;y$2=y$1 - 15/30h;
 x$5=1/2[x$1,x$2];
 y$5=1/2[y$1,y$2];
 draw z$1{dir 250}..tension 1.4..z$5..{dir 250}z$2;
enddef;
%
%def do_nothing = 
%beginchar (0, em#, 1.0cap#, 0); "nothing";
%endchar;enddef;
%
% FLAGS
%
def do_whole =
beginchar ("W", 1.15em#, 1.0cap#, 0);" whole note";
 pickup pencircle xscaled 1.45 wid yscaled .3 wid rotated 5;
 x0=0.13w;x1=.88w;
 y0=0.28h;y1=.358h;
 draw z0..tension 1.13..z1..tension 1.13..cycle;
endchar;enddef;
%
def do_half =
beginchar ("w", em#, 1.0cap#, 0);" half note";
 pickup pencircle xscaled 1.2 wid yscaled .5 wid rotated 7;
 x2=0.14w;x1=.5w;x0=.92w;
 y2=0.3h; y1=.63h;y0=.45h;
	x3=x1;y3=0.03h;
	x4=x0;y4=.35h;
	x5=x0;y5=2.1h;
	penpos0(.9wid, 20);
	penpos1(.8wid, 90);
	penpos2(1.3wid, 180);
	penpos3(1.1wid, 210);
	penpos4(.8wid, 350);
	penpos5(.8wid, 350);
penstroke z0e..tension 1.03..z1e..tension 1.03..z2e..z3e..z4e..tension 6..z5e;
endchar;enddef;
%
def do_long =
beginchar ("L", 2.4em#, 1.0cap#, -cap#);" a long";
  pickup pencircle xscaled .2wid yscaled 1.8wid;
  x0=x2=0w; x1=x3=1w; 
  top y0=top y1=.8h;bot y2=bot y3=0.1h;
  draw z0..z1; draw z2..z3;
  pickup pencircle xscaled .4wid yscaled .2wid;
  x4=x5=0w; x6=x7=w;
  y4=-.1h;y6=d; y5=y7=1h;
  draw z4..z5; draw z6..z7;
endchar;enddef;

def do_breve =
beginchar ("J", 1.2em#, 1.0cap#, 0);" a breve";
  pickup pencircle xscaled .2wid yscaled 1.8wid;
  x0=x2=0w; x1=x3=1w; 
  top y0=top y1=.8h;bot y2=bot y3=0.1h;
  draw z0..z1; draw z2..z3;
  pickup pencircle xscaled .4wid yscaled .2wid;
  x4=x5=0w; x6=x7=w;
  y4=y6=-.1h; y5=y7=1h;
  draw z4..z5; draw z6..z7;
endchar;enddef;

%
%
def do_note_body =
   	x1=.95w; y1=.63w;
	fill (x1,.y1)..(.5w, .8w)..(0.05w,.17 w)..(.5w,0)..cycle; 
  pickup pencircle scaled .8 wid;
  rt x2=w;;y2=h; 
  x3=x2;y3=y1 - .13w;
  draw z3..z2;  % stem
%  labels(1,2);
enddef;
%
def do_mod_open =
beginchar (211, 1.0em#, 1.0cap#, 0);" half note body"; 
 pickup pencircle xscaled 1.2 wid yscaled .2 wid rotated 145;
 	lft x2=0w;x1=.7w;rt x0=w;x3=.3w;
 	y2=0.2h; top y1=.6h;y0=.4h;
	y3=0.0h;

%	penpos0(.9wid, 20);
%	penpos1(.8wid, 90);
%	penpos2(1.3wid, 180);
%	penpos3(1.1wid, 210);
%penstroke z0e..tension 1.03..z1e..tension 1.03..z2e..z3e..cycle;
	draw z0..z1..tension 1.1..z2..z3..tension 1.1..cycle;
endchar;enddef;
%
def do_open =
beginchar (212, em#, 1.0cap#, 0);" half note body"; 
 pickup pencircle xscaled 1.2 wid yscaled .5 wid rotated 7;
 	x2=0.14w;x1=.5w;x0=.88w;
 	y2=0.3h; y1=.67h;y0=.47h;
	x3=x1;y3=0.0h;
	penpos0(.9wid, 20);
	penpos1(.8wid, 90);
	penpos2(1.3wid, 180);
	penpos3(1.1wid, 210);
penstroke z0e..tension 1.03..z1e..tension 1.03..z2e..z3e..cycle;
endchar;enddef;
%
def do_rflag =
beginchar (213, .9em#, .7cap#, 0);" down quarter note flag";
   	x1=0;  y1=.5h;    x2=.5w;y2=h - .1h;
	x3=.85w;y3=h +.4h; x4=.89w;y4=h + .8h;
	x5=.7w;y5=h + 1.7 h;
	penpos1 ( h, -90);penpos2 (.35h, -60);penpos3 (.18h, 0);
	penpos4 (.14h, 0);penpos5 (.08h, -330);
	penstroke z1e..z2e..z3e..z4e..z5e;
endchar;enddef;
%
def do_fflag =
beginchar (214, .9em#, .7cap#, 0);" up quarter note flag";
   	x1=0;  y1=.5h;    x2=.5w;y2=.1h;
	x3=.85w;y3= -.4h; x4=.89w;y4= -.8h;
	x5=.7w;y5=-1.7 h;
	penpos1 ( h, 90);penpos2 (.35h, 60);penpos3 (.18h, 0);
	penpos4 (.14h, 0);penpos5 (.08h, 330);
	penstroke z1e..z2e..z3e..z4e..z5e;
endchar;enddef;
%
def do_sol =
beginchar (215, em#, 2.1cap#, 0);" quarter note body"; 
   	x1=.95w; y1=.63w;
	fill (x1,.y1)..(.5w, .8w)..(0.05w,.17 w)..(.5w,0)..cycle;
endchar;enddef;
%
def do_solid =
beginchar (202, em#, 2.1cap#, 0);" quarter note"; 
  do_note_body;
endchar;enddef;
%
def do_flg(suffix $)(expr the_top) =
  pickup flag_pen;
  x$1=.95w;x$2=1.8w;top y$1=the_top;y$2=y$1 - 12/30h;
  draw z$1{dir 250}..tension 1.4..{dir 250}z$2;
enddef;
%
def do_solid_o =
beginchar (203, em#, 2.1cap#, 0);" eighth note"; 
  do_note_body;
  do_flg(2, h);endchar;enddef;
%
def do_solid_oo =
beginchar (204, em#, 2.1cap#, 0);" sixteenth note"; 
  do_note_body;
  do_flg(1, h); do_flg(2, .8h);
endchar;enddef;
%
def do_solid_ooo =
beginchar (205, em#, 2.1cap#, 0);" thirty second note"; 
  do_note_body;
  do_flg(1, h);
  do_flg(2, .8h); do_flg(3, .6h);
endchar;enddef;
%
def do_solid_oooo =
beginchar (206, em#, 2.1cap#, 0);" sixty fourth note"; 
  do_note_body;
  do_flg(1, h);
  do_flg(2, .83h); do_flg(3, .66h);do_flg(4, .49h);
endchar;enddef;
%
def do_solid_ooooo =
beginchar (207, em#, 2.1cap#, 0);" 128 note"; 
  do_note_body;
  do_flg(1, h);
  do_flg(2, .83h); do_flg(3, .66h);do_flg(4, .49h);
  do_flg(5, .33h);
endchar;enddef;
%
%
def do_line =
beginchar (194, em#, 2.1cap#, 0);" no flags";
 pickup stem_pen;
 do_v_bar;
endchar;enddef;
%
def do_one =
beginchar (195, 1.35em#, 2.1cap#, 0);" one flags";
 do_v_bar; do_a_big_flag(5, h);endchar;enddef;
%
def do_two =
beginchar (196, 1.15em#, 2.1cap#, 0);" two flags";
 do_a_med_flag(5, h);do_a_med_flag(7, .8h);do_v_bar;
endchar;enddef;
%
def do_three =
beginchar (197, .9em#, 2.1cap#, 0);" three flags";
 do_a_flag(5, h);do_a_flag(7, .8h);
 do_a_flag(9, .6h);
 do_v_bar;
endchar;enddef;
%
def do_four =
beginchar (198, .85em#, 2.1cap#, 0);" four flags";
 do_a_flag(5, h);do_a_flag(7, .8h);
 do_a_flag(9, .6h);
 do_a_flag(11, .4h);
 do_v_bar;
endchar;enddef;
%
def do_five =
beginchar (199, .8em#, 2.1cap#, 0);" five flags";
 do_a_flag(5, h);do_a_flag(7, .8h);
 do_a_flag(9, .6h);
 do_a_flag(11, .4h);
 do_a_flag(13, .2h);
 do_v_bar;
endchar;enddef;
%
def ii_flag(suffix $)(expr the_top) =
 pickup pencircle xscaled 1.0 wid yscaled .3wid rotated 75;
 x$1=v_bar;x$2=w;
 top y$1=the_top;
 y$2=y$1 - 4/30h;
 draw z$1..z$2;
enddef;
def do_ii_line =
beginchar (242, em#, 2.1cap#, 0);" no flags";
 pickup stem_pen;
 do_v_bar;
endchar;enddef;
%
def do_ii_one =
beginchar (243, em#, 2.1cap#, 0);" one flags";
 do_v_bar; ii_flag(5, h);endchar;enddef;
%
def do_ii_two =
beginchar (244, em#, 2.1cap#, 0);" two flags";
 ii_flag(5, h);ii_flag(7, .8h);do_v_bar;
endchar;enddef;
%
def do_ii_three =
beginchar (245, em#, 2.1cap#, 0);" three flags";
 ii_flag(5, h);ii_flag(7, .8h);
 ii_flag(9, .6h);
 do_v_bar;
endchar;enddef;
%
def do_ii_four =
beginchar (246, em#, 2.1cap#, 0);" four flags";
 ii_flag(5, h);ii_flag(7, .8h);
 ii_flag(9, .6h);
 ii_flag(11, .4h);
 do_v_bar;
endchar;enddef;
%
def do_ii_five =
beginchar (247, em#, 2.1cap#, 0);" five flags";
 ii_flag(5, h);ii_flag(7, .8h);
 ii_flag(9, .6h);
 ii_flag(11, .4h);
 ii_flag(13, .2h);
 do_v_bar;
endchar;enddef;

%
%
%	Italian style flags
%	like old music
%
if unknown bodyht : bodyht# = 3wid#;fi;% actually half
if unknown bodyw  : bodyw# = .8 * bodyht#;fi;
define_pixels(bodyht, bodyw);
%
def do_open_body =
 pickup pencircle xscaled 1.8 wid yscaled .5 wid rotated 50;
 x0=.1w;x2=.9w;x1=.52w;x3=.48w; 
 y0=.195h;y1=0h;y2=.175h;y3=.35h;
% was y0=.2h;y1=0h;y2=y0;y3=.4h;
 draw z0..z1;draw z1..z2; draw z2..z3; draw z3..z0;
enddef;
%
def do_closed_body =
 pickup pencircle scaled .1 wid;
 	x0=good.x(.5w - bodyw); 
	x1= x3=good.x(.5w); 
	x2=good.x(.5w + bodyw);
 y0=good.y(bodyht);y1=0h;y2=y0;y3=2 bodyht;
 fill z0--z1--z2--z3--cycle
enddef;
%
if unknown stem: stem# = .9wid#;fi;
if unknown flare: flare# = 1.8 wid#;fi;
define_pixels(stem, flare);
def do_stem =
	x5 = x6 = x8 = .505w; x7=.44w;
	y5=.29h;y6=h;y8=.8h; y7=h;
	penpos5(stem, 0);
	penpos8(stem, 0);
	penpos6(flare, 25);
	penstroke z5e..tension 5..z8e..z6e;
enddef;
%
def do_r_stem =
 	x5=x6=x8=.505w; 
	y5=.033h;
	y8=-.41h;
	y6=-.6h;
	penpos5(stem, 0);
	penpos8(stem, 0);
	penpos6(flare, 25); 
	penstroke z5e..tension 10..z8e..z6e;
	
enddef;
%
def do_i_flag(suffix $)(expr the_top) =
 pickup pencircle xscaled 1.4 wid yscaled .5 wid rotated 30;;
 x$1=.5w;x$2=.9w;x$3=x$1;
 top y$1=the_top+0.03h;y$2=y$1 - 8/30h;
 y$3=y$1 - .5h;
 draw z$1..tension 1.4..{dir 250}z$2..z$3;
enddef;
%
%
def do_ri_flag(suffix $)(expr the_top) =
 pickup pencircle xscaled 1.4 wid yscaled .5 wid rotated 30;;
 x$1=.5w;x$2=.9w;x$3=x$1;
 top y$1=the_top+0.03h;y$2=y$1 + 8/30h;
 y$3=y$1 + .5h;
 draw z$1..tension 1.4..{dir 110}z$2..z$3;
enddef;
%
def do_is_flag(suffix $)(expr the_top) =
 pickup pencircle xscaled 1.4 wid yscaled .5 wid rotated 30;
 x$1=.5w;x$2=w;
 top y$1=the_top+0.03h;y$2=y$1 - 4/30h;
 draw z$1..tension 1.4..{dir 300}z$2;
enddef;
%
def do_ris_flag(suffix $)(expr the_top) =
 pickup pencircle xscaled 1.4 wid yscaled .5 wid rotated 150;
 x$1=.5w;x$2=w;
 top y$1=the_top-0.03h;y$2=y$1 + 4/30h;
 draw z$1..tension 1.4..{dir 70}z$2;
enddef;
%
% h used to be 2.1cap#
def do_i_one =
beginchar (216, 1.1em#, 2.5cap#, 0); "Italian flag"; do_open_body;
endchar;enddef;
%
def do_ri_one =
beginchar (224, 1.1em#, 2.5cap#, 0); "Italian flag"; do_open_body;
endchar;enddef;
%
def do_i_two =
beginchar (217, 1.1em#, 2.5cap#, 0); "Italian flag"; do_open_body; do_stem;
endchar; enddef;
%
def do_ri_two =
beginchar (225, 1.1em#, 2.5cap#, 0); "Italian flag"; do_open_body; do_r_stem;
endchar; enddef;
%
def do_i_three =
beginchar (218, 1.1em#, 2.5cap#, 0); "Italian flag"; do_closed_body; do_stem;
endchar; enddef;
%
def do_ri_three =
beginchar (226, 1.1em#, 2.5cap#, 0); "Italian flag"; do_closed_body; do_r_stem;
endchar; enddef;
%
def do_i_four =
beginchar (219, 1.1em#, 2.5cap#, 0); "Italian flag";
 do_closed_body; do_stem; do_i_flag(10,.98h);
endchar; enddef;
%
def do_ri_four =
beginchar (227, 1.1em#, 2.5cap#, -1.5cap#); "Italian flag";
 do_closed_body; do_r_stem; do_ri_flag(10,d);
endchar; enddef;
%
def do_i_five =
beginchar (220, 1.1em#, 2.5cap#, 0); "Italian flag";
 do_closed_body; do_stem; do_is_flag(10,h); do_is_flag(20,.72h);
endchar; enddef;
% 
def do_ri_five =
beginchar (228, 1.1em#, 2.5cap#, -1.26cap#); "Italian note reversed 2 flags";
 do_closed_body; do_r_stem; do_ris_flag(10,d); do_ris_flag(20,.62d);
endchar; enddef;
% 
def do_i_six =
beginchar (221, 1.1em#, 2.5cap#, 0); "Italian flag";
 do_closed_body;do_stem;do_is_flag(10,h);do_is_flag(20,.8h);do_is_flag(30,.6h);
endchar; enddef;
% 
def do_ri_six =
beginchar (229, 1.1em#, 2.5cap#, -1.26cap#); "Italian note reversed 3 flags";
 do_closed_body;do_r_stem;
 do_ris_flag(10,d);do_ris_flag(20,.62d);do_ris_flag(30,0.27d);
endchar; enddef;
% 
def do_i_seven =
beginchar (222, 1.1em#, 2.5cap#, 0); "Italian flag";
 do_closed_body;
 do_stem;
 do_is_flag(10,h);
 do_is_flag(20,.87h);
 do_is_flag(30,.73h);
 do_is_flag(40,.6h);
endchar; enddef;
% 
def do_i_eight =
beginchar (223, 1.1em#, 2.5cap#, 0); "Italian flag";
 do_closed_body;do_stem;
 do_is_flag(10,h);
 do_is_flag(20,.87h);
 do_is_flag(30,.73h);
 do_is_flag(40,.6h);
 do_is_flag(50,.46h);
endchar;enddef;
%
% italian flags with shorter stems
%
def do_i_open_body =
 pickup pencircle xscaled 1.6 wid yscaled .5 wid rotated 50;
 x0=.1w;x2=.9w;x1=x3=.5w; 
 y0=.2h;y1=0h;y2=y0;y3=.4h;
 draw z0..z1;draw z1..z2; draw z2..z3; draw z3..z0;
enddef;
%
def do_i_closed_body =
 pickup pencircle scaled .1 wid;
 x0=.05w; x1= x3=.5w; x2=.95w;
 y0=.2h;y1=0h;y2=y0;y3=.43h;
 fill z0..tension 18..z1..tension 18..z2..tension 18..z3..tension 18..cycle;
enddef;
%
def do_i_stem =
 x5=x6=.5w; y5=.35h;y6=h;
 penpos5(1.5wid, 50);penpos6(1.8 wid, 30); penstroke z5e..z6e;
enddef;
%
def do_iii_two =
beginchar (231, em#, 2.1cap#, 0); "Italian flag"; do_i_open_body; do_i_stem;
endchar; enddef;
%
def do_iii_three =
beginchar (232, em#, 2.1cap#, 0); "Italian 0 flag"; 
do_i_closed_body; do_i_stem;
endchar; enddef;
%
def do_iii_four =
beginchar (233, em#, 2.1cap#, 0); "Italian 1 flag";
 do_i_closed_body; do_i_stem; do_i_flag(10,h);
endchar; enddef;
%
def do_iii_five =
beginchar (234, em#, 2.1cap#, 0); "Italian  2 flag";
 do_i_closed_body; do_i_stem; do_is_flag(10,h); do_is_flag(20,.82h);
endchar; enddef;
% 
def do_iii_six =
beginchar (235, em#, 2.1cap#, 0); "Italian 3 flag";
 do_i_closed_body;do_i_stem;do_is_flag(10,h);do_is_flag(20,.82h);do_is_flag(30,.66h);
endchar; enddef;
% 
def do_iii_seven =
beginchar (236, em#, 2.1cap#, 0); "Italian 4 flag";
 do_i_closed_body;do_i_stem;
 do_is_flag(10,h);do_is_flag(20,.85h);
 do_is_flag(30,.7h);do_is_flag(40,.55h);
endchar; enddef;
% 
%
%
%	END FLAGS
%
%	LETTERS
%
def do_a =
beginchar ("a", .8em#, 1.0cap#, 0);"letter a";
 pickup bigger_lute_pen;
 x1=w;y1=.6h;
 x2=.4w;y2=0;
 x3=.2w;y3=.4;
 x4=.8w;y4=mid_bar;
 x5=w;y5=-.06h;
 draw z1{dir 260}..{left}z2..tension 1.3..z3..tension 1.4..z4..tension 1.5..{dir 290}z5;
endchar;enddef;
%
def do_b =
beginchar ("b", .7em#, 1.0cap#, 0);"letter b";
 pickup bigger_lute_pen;
 x1=-.1w;y1=.4h;
 x2=.8w;y2=.9h;
 x3=.3w;y3=.9h;
 x4=.1w;y4=.2h;
 x5= .92w;y5=.2h;
 x6=.2w;y6=.4h;
 draw z1..tension 1.5..z2..z3..tension 1.8..z4..z5..z6;
endchar;enddef;
%
def do_c =
beginchar ("c", .8em#, 1.0cap#, 0);"letter c";
 pickup lute_pen;
 x1=w;y1=mid_bar;
 x2=.13w;y2=.6h;
 x3=.2w;y3=.12h;
 x4=.4w;y4=.59h;
 x5=.4w;y5=-.05h;
% draw z1..tension 1.6..z2..tension 1.6..z3;
 penpos1(.5wid, 45); penpos2(2.1wid,50);penpos3(.8wid,30);
 penpos4(1.8wid,45);penpos5(.6wid,30);
 penstroke z1e..z4e..z2e;
 penstroke z2e..tension 2..z3e..{dir 345}z5e;
endchar;enddef;
%
def do_d =
beginchar ("d", em#, 1.0cap#, 0);"letter d";
 pickup pencircle xscaled 1.2wid yscaled .3wid rotated 45;
 x1=.7w;y1=.45h;
 x2=.4w;y2=0;
 x3=.7w;y3=.1h;
 x4=-.1w;y4=1.4h;
 x5=.75w;y5=.14h;
 draw z1{dir 120}..tension 1.1..z2..tension 1.1..z3;
 penpos1(1.08wid, 30); penpos2(1.2wid, 0); penpos4(1.8wid, 30);
 penpos5(.8wid, 20);
 penstroke z4e..z1e..z5e..{dir 180}z2e;
endchar;enddef;
%
def do_D =
beginchar ("D", em#, 1.0cap#, 0);"letter D bent over";
 pickup pencircle xscaled 1.2wid yscaled .2wid rotated 50;;
 x1=.7w;y1=.45h;
 x2=.4w;y2=0;
 x3=.5w;y3=.6h;
 x4=-.9w;y4=0.90h;
 draw z1..z2..tension 1.2..z1;
 penpos3(1.2 wid, 45);penpos4(2.2wid, 45);
 penstroke z3e{dir 175}..z4e;
% draw z3{dir 175}..tension 1.1..z4;
endchar;enddef;
%
def do_e =
beginchar ("e", em#, 1.0cap#, 0);"letter e";
 pickup bigger_lute_pen;
 x1=.3w;y1=.1h;
 x2=.7w;y2=mid_bar;
 x4=1.2w;y4=.3h;
 draw z1..tension 2.5..z2;draw z2..tension 0.9..z1..tension 1.2..z4;
endchar;enddef;
%
def do_f =
beginchar ("f", em#, 1.0cap#, .76cap#);"letter f";
 pickup pencircle xscaled 1.4wid yscaled .4wid rotated 30;
 x1=.1;y1=.15h;
 x2=.8w;y2=.4h;
 x5=-.5w;y3=y5=-.87d;   % -.7h;
 x3=-.1w;
 x4=.7w;y4=.85h;
 x6=w;y6=y4;
 draw z1..tension 2.5..z2;draw z5..z3..tension 3.0..z4..z6;
endchar;enddef;
%
def do_F =
beginchar ("F", em#, 1.0cap#, 2cap#);"letter F";
 pickup pencircle xscaled 1.4wid yscaled.4wid rotated 40;
 x1=-.15w;y1=.95h;
 x2=.6w;y2=.68h;
 x3=.4w; y3=.325h;
 x6=.9w;y6=.15h;
 x4=w;y4=.0h;
 x5=x1;y5=-d;%-2h;
 draw z1..tension 3..z2..z3..tension 3..z6..z4;
 pickup pencircle xscaled .8wid yscaled .3wid rotated 30;
 draw z1..z5;
endchar;enddef;
%
def do_G =
beginchar ("G", .9em#, 1.0cap#, 1.0cap#);"letter g";
 pickup pencircle xscaled 1.6 wid yscaled .3wid rotated 40;
 x1=.9w;y1=.6h;
 x2=.2w;y2=.05h;
 x3=.93w;y3=.6h;
 x5=.5w;y5=0.05h;
 x9=.9w;y9=.35h;
 x8=.7w;y8=-.15d;%-.15h;
 x4=.6w;y4=-.9d;%-.9h;
 x6=.2w;y6=y4;
 x7=.3w;y7=-.2d;%-.2h;
 draw z1{dir 200}..tension 1.1..z2..z5..z3;
 penpos3(.4wid, 40);penpos8(1.2wid,  30);penpos4(1.2wid, 45);
 penpos6(1.2wid, 30);penpos7(1.2wid, 30);
 draw z9..z8..z4..z6..{dir 45}z7;
endchar;enddef;
%
def do_g =
beginchar ("g", .8em#, 1.0cap#, 0.5cap#);"letter g";
 pickup bigger_lute_pen;
 x1=.2w;y1=.4h;
 x2=.2w;y2=.05h;
 x3=.87w;y3=mid_bar +.04h;
 x8=.87w;y8=mid_bar;
 x5=.8w;y5=y2;
 x4=.7w;y4=-.6d;%-.3h;
 x6=-1.3w;y6=y4;
 draw z8{dir 175}..tension 1.0..z1..tension 1.3..z2..tension 1.9..z5;
 draw z3..tension 1.8..z4..tension 1.8..{dir 160}z6;
endchar;enddef;
%
def do_h =
beginchar ("h",.7em#, 1.0cap#, 0);"the letter h";
 pickup bigger_lute_pen;
 x1=0;y1=1.1h;
 x2=0;y2=0;
 z3 = .4[z2,z1];
 x4 = .92w; y4=y3;
 x5 = .92w; top y5=0;	 
 draw z1..z2;
 draw z3..z4..{dir 300}z5;
endchar;enddef;
%
def do_H =
beginchar ("H", em#, 1.0cap#, -cap#);"the letter H";
  pickup bigger_lute_pen;
  x1=0;y1=.2h;  x2=.8w;y2=.6h;
  x3=.1w;y3=0;  x4=.4w;y4=.3d;
  x5=.15w;y5=d;  x6=x7=w;y6=y5;
  y7=.8d;
  x8=x7;y8=.3d;
  draw z1..tension 1.9..z2..z1{dir 270}..z3{dir 25}..z4..z5..z6..z7..z8;
endchar;enddef;
%
def do_i =
beginchar ("i",.7em#, 1.0cap#, 0);"the letter i";
 pickup pencircle xscaled 1.4 wid yscaled .4 wid rotated 30;;
 x2=.35w;
 x1=x3=x4=.5w;
 y1=.50h;y2=-.05h;
 x5=x2; y5=y1;
 x6=.8w;y6=.07h;
 y3=.85h;y4=.75h;
 draw z5..tension 2..z1..tension 4..z2..tension 3..{dir 100}z6;
 draw z3..z4;
endchar;enddef;
%
def do_I =
beginchar ("I",.9em#, 1.0cap#, -cap#);"the letter I or Y";
  pickup bigger_lute_pen;
  x1=.3w;y1=.1h;  
  x2=.8w;y2=.8h;
  x3=.4w;y3=0;  
  x4=.2w;y4=.3d;
  x5=.2w;y5=1.1d;
  x6=w;y6=y5;
  x7=w;y7=.2d;
  x8=0;y8=.8h;
  x10=.175w;y10=.5h;
  x9=.68w;y9=.1h;
  x11=.55w;y11=.2d;
  draw z2{dir 290}..z11..tension 1.3..z5..tension 1.0..
  z6..{dir 80}z7;
  penpos8(1.4wid, 45);penpos10(1.4 wid , 45);penpos9(.5 wid, 75);
  penstroke z8e{dir 240}..z10e..z9e;
%  draw z8{dir 240}..z10..z9;
endchar;enddef;
%
def do_j =
beginchar ("j",.7em#, 1.0cap#, 0);"the letter j";
 pickup bigger_lute_pen;
 x2=.25w;
 x1=x3=x4=x5=.5w;
 y1=.5h;y2=-.5h;y5=0h;
 y3=.8h;y4=.7h;
 draw z1..tension 2..z5..{dir 220}z2;draw z3..z4;
endchar;enddef;
%
def do_k =
beginchar ("k",.9em#, 1.0cap#, 0);"the letter k";
 pickup bigger_lute_pen;
 x11=.3w;y11=1.3h;
 lft x1=0.16w; y1=1.h;
 x2=x3=x1;y2=0;
 x4=.87w;y4=.73h;
 lft x33=0;y33=y3;
 x5=w;
 y3=.4h;
 y5=-0.156h;
 draw z11..z1..tension 4..z2;
 draw z3{dir 80}..z4{dir 270}..{dir 180}z3..z33;
 draw z3{dir 330}..{dir 335}z5;
endchar;enddef;
%
def do_l =
beginchar ("l", .6em#, 1.0cap#, 0);"the letter l";
pickup bigger_lute_pen;
pickup pencircle xscaled 1.4wid yscaled .3 wid rotated 55;
 if unknown l_top# : l_top# = 1.3h;fi;
 lft x1= .4w;y1=.45 l_top#;
 rt  x2= .9w;y2=.76 l_top#;
% z3=whatever[(0,l_top#),(w,l_top#)];
 x3=.5w;top y3=l_top#;
 lft x4= 0w; y4=y2;
 x5=x4 + .08w;y5=.5[y4,y6];
 x6=x4;bot y6=0;
 rt x7=w;y7=y6+ .02h;
 draw z1{dir 55}..tension 1.6..z2..z3..z4..z5..z6&z6{dir 55}..{dir 55}z7;
 
endchar;enddef;
%
def do_m =
beginchar ("m", 1.2em#, 1.0cap#, 0);"the letter m";
pickup pencircle xscaled 1.5wid yscaled .3 wid rotated 50;
 lft x1=0;y1=.62h;
 lft x2=.08w;y2=mid_bar-.05h;
 rt x6=w;y6=y1;
 x22=x2;y22=y6;
 x3=x2; bot y3=0;
 x4=.5[x2,x6];y4=y2;
 x44=x4;y44=y6;
 x5=x4;y5=y3;
 x66=x6;y66=y44;
 x7=x6;y7=y3;
 draw z1..z22..tension 4..z3;
 draw z2{dir 60}..tension 1.2..z44..tension 4..z5;
 draw z4{dir 60}..tension 1.2..z66..tension 4..z7;
endchar;enddef;
%
def do_n =
beginchar ("n", .7em#, 1.0cap#, 0);"the letter n";
pickup pencircle xscaled 1.5wid yscaled .3 wid rotated 50;
 lft x1=0;y1=.62h;
 lft x2=.08w;y2=mid_bar-.05h;
 rt x6=w;y6=y1;
 x22=x2;y22=y6;
 x3=x2; bot y3=0;
 rt x4=w;y4=y2;
 x44=x4;y44=y6;
 x5=x4;y5=y3;
 draw z1..z22..tension 4..z3;
 draw z2{dir 60}..tension 1.7..z44..tension 4..z5;
endchar;enddef;
%
def do_o =
beginchar ("o", .9em#, 1.0cap#, 0);"the letter o";
 pickup bigger_lute_pen;
 x1=0; x2=w;
 y1=y2=.4h;
 draw z1..z2..cycle;
endchar;enddef;
%
def do_p =
beginchar ("p", .7em#, 1.0cap#, .6cap#);"the letter p";
  pickup bigger_lute_pen;
  x1=0;y1=.5h;
  x2=0;y2=-d;%-.6h;
  x3=w;y3=.5h;
  draw z1..z3..z1..tension 4..z2;
endchar;enddef;
%
% 		END LETTERS
%
%		DOTS
%
%
 if unknown dw: dw# = 1.5wid#;fi;
 define_blacker_pixels(dw);
 def my_dot(suffix $)(expr wid)(expr hi) = 
 fill fullcircle scaled dw shifted (wid, hi);
enddef;
%
def s_dot(suffix $)(expr wid)(expr hi) = 
 x$1=wid -.10w;x$2=x$4=wid;x$3=wid +.10w;
 y$1=y$3= hi;y$2= hi-.09h;y$4= hi+.09h;
 fill z$1..z$2..z$3..z$4..cycle;
enddef;
%
% a dot centered on the left boundary
%
def do_c_dot =  % used in repeats and for ornaments
beginchar ("Z", 1.0cap#, 1.0cap#, 0);"a left centered dot";
 my_dot(1, 0.0w, 0.5h);
endchar;enddef;
%
%
def do_dot =  % for fingerings and dotted flags
beginchar (".", 1.0cap#, 1.0cap#, 0);"a dot";
 my_dot(1, 0.3w, 0.5h);
endchar;enddef;
%
def do_two_dot =
beginchar (":", em#, 1.0cap#, 0);"two dots";
 my_dot(1, 0.6w, 0.5h);
 my_dot(2, 0.1w, 0.5h);
 endchar;enddef;
%
def do_three_dot =
beginchar (";", em#, 1.0cap#, 0);"three dots";
 my_dot(1, 0.6w, 0.4h);
 my_dot(2, 0.1w, 0.4h);
 my_dot(3, 0.35w, 0.7h);
 endchar;enddef;
%
def do_four_dot =
beginchar ("$", em#, 1.0cap#, 0);"three dots";
 my_dot(1, 0.6w, 0.15h);
 my_dot(2, 0.1w, 0.15h);
 my_dot(3, 0.6w, 0.45h);
 my_dot(4, 0.1w, 0.45h);
 endchar;enddef;
%
def do_thumb =
beginchar ("|", em#, 1.0cap#, 0);"|";
 pickup pencircle xscaled .6wid yscaled .6wid;
 x1=.5w;x2=x1;
 y1=.2h;y2=.7h;
 draw z1..z2;
endchar;enddef;
%
def c_body(expr thick) =
% pickup pencircle xscaled 1.6 yscaled .4wid rotated 20;
 x0=1.05w;y0=.73h;
 rt x1=.97w;rt x4=w;
 y1=.62h;
 y2=.6h;
 y3=.4h;
 y4=.36h;
 x2=x3=.15w;
 x5=x6=.5w; top y5=.8h;bot y6=.2h;
 lft x7=0; y7=.5h;
% draw z0{dir 235}..z1;
% draw z1{dir 110}..z5..z2..z3..z6..{dir 65}z4;
 penpos0(1.wid, 25);
 penpos1(thick, 25);
 penpos2(thick, 135);
 penpos3(thick, 225);
 penpos4(thick, 315);
 penpos5(.5wid, 90);
 penpos6(.6wid, 270);
 
 penstroke z0e{dir 240}..z1e;
 penstroke z1e..z5e..z2e..z3e..z6e..{dir 65}z4e;
enddef;
%
def do_C =
beginchar ("U", 1.5em#, 2.2cap#, 0);"C";
  c_body(1.6 wid);
endchar;enddef;
%
def do_big_C =
beginchar (19, 2.15em#, 3.5cap#, 0);"C";
  c_body(1.9wid);
endchar;enddef;
%
def do_big_cut_C =
beginchar (20, 2.15em#, 3.5cap#, 0);"Cut time";
 c_body(1.9wid);
 pickup pencircle xscaled 1.2wid yscaled .3wid rotated 45;
 x15=x16=.57w;
 bot y15=0h;top y16=h;
 draw z15..z16;
endchar;enddef;
%
def do_cut_C =
beginchar ("u", 1.5em#, 2.2cap#, 0);"Cut time";
 c_body(1.6wid);
 pickup pencircle xscaled 1.3wid yscaled .3wid rotated 45;
 x15=x16=.57w;
 bot y15=0h;top y16=h;
 draw z15..z16;
endchar;enddef;
%
def do_fermata_dot  =
beginchar (92 , 3em#, 3em#, 0);"a fermata";
 pickup pencircle scaled 2.2 wid;
 x1=.5w;y1=.5h;
 draw z1;
endchar;enddef;
%
def do_fermata =
beginchar ("Y", 3em#, 3em#, 0);"a fermata";
 pickup pencircle scaled 2.2 wid;
 x1=.5w;y1=.5h;
% draw z1;
 x2=0w; y2=.5h;
 x3=.5w;y3=h;
 x4=w;y4=y2;
pickup pencircle xscaled 2.2 wid yscaled 1.0 wid rotated 35;
draw z2..z3..z4;
endchar;enddef;
%
def do_b_fermata =
% beginchar ("y", .3in#, .3in#, 0);"a fermata like M Board made";
beginchar ("y", 3em#, 3em#, 0);"a fermata like M Board made";
 pickup pencircle scaled 2.2 wid;
 x1=.5w;y1=.5h;
 draw z1;
 x2=0w; y2=.5h;
 x3=.5w;y3=h;
 x4=w;y4=y2;
 x5=x3;y5=0h;
 pickup pencircle xscaled 2.2 wid yscaled 1.0 wid rotated 35;
 penpos5(.5 wid,45);penpos2(.6 wid,0);penpos3(2.0 wid ,300);penpos4(2.9 wid,230);
 penstroke z5e..z2e..z3e..z4e;

endchar;enddef;
%
def do_tail =
beginchar ("Q", 5.5em#, 5.4cap#, 0);"a tail";
  pickup pencircle xscaled .7wid yscaled .3 wid rotated 20;
  thin_pen := savepen;
  pickup pencircle xscaled 1.8wid yscaled .3 wid rotated 40;
  thick_pen := savepen;
  x1=0;y1=0;
  x2=.15w;y2=h;
  x3=.3w;y3=.16h;
  x4=.46w;y4=.85h;
  x5=.59w;y5=.33h;
  x6=.66w;y6=.65h;
  x7=.72w;y7=.45h;
  x8=.92w;y8=.54h;
  x12=w;y12=.7h;
  pickup thin_pen;
  draw z1{dir 60}..tension 7..{dir 0}z2;
  pickup thick_pen;
  draw z2{dir 0}..tension 7..{dir 0}z3;
  pickup thin_pen;
  draw z3{dir 0}..tension 5..{dir 0}z4;
  pickup thick_pen;
  draw z4{dir 0}..tension 5..{dir 0}z5;
  pickup thin_pen;
  draw z5{dir 0}..tension 5..{dir 0}z6;
  pickup thick_pen;
  draw z6{dir 0}..tension 5..{dir 0}z7..
  z8...z12;
endchar;enddef;
%
def do_another_tail =
beginchar ("q", 5em#, 5.4cap#, 0);"another tail";
  pickup pencircle xscaled 1.7wid yscaled .3wid rotated 40;
  x1=0;y1=0;
  x2=.15w;y2=h;
  x3=.3w;y3=.2h;
  x4=.45w;y4=.8h;
  x5=.6w;y5=.4h;
  x6=.7w;y6=.6h;
  draw z1{dir 90}..tension 6..z2..tension 6..z3..tension 6..z4..tension 6..z5..tension 6..z6;
endchar;enddef;
%
%
def do_yet_another_tail =
beginchar (191, 4.em#, 5.4cap#, 0);"yet another tail";
  pickup pencircle xscaled 1.7wid yscaled .3wid rotated 20;
  x1=0;y1=1.02h;
  x2=.2w;y2=0;
  x3=.42w;y3=.98h;
  x4=.64w;y4=.0h;
  x5=.8w;y5=.94h;
  x6=.89w;y6=.06h;
  rt x7=w;y7=-.07h;
  x8=.6[x5, x6];y8=.6[y5,y6];
  draw z1{down}..tension 6..z2..tension 6..z3{left}..tension 6..z4..
  tension 5.2..z5{left}..tension 5..z8..tension5..z7{left}..z6{up};
endchar;enddef;
%
%
def do_hold =
beginchar ("V", 5em#, 5.4cap#, 0);"hold the last note";
  pickup lute_pen;
endchar;enddef;
%
%		ORNAMENTS
%
def do_relish_a=
beginchar ("#", em#, 1.0cap#, 0);"a hash relish";
  pickup lute_pen;
  x1=x2=.35w;
  x3=x4=.65w;
  y1=y3=.8h;
  y2=y4=.2h;
  x5=x7=.2w;
  x6=x8=.8w;
  y5=y6=.35h;
  y7=y8=.65h;
  draw z1..z2;draw z3..z4;
  draw z5..z6;draw z7..z8;
endchar;enddef;
%
def do_flat=
beginchar ("?", .55em#, 1.0cap#, 0);"a flat char";
  pickup lute_pen;
  lft x0=lft x1=0;
  y0=0h;y1=1.5h;
  draw z0..z1;
  lft x2=x0;y2=.5h;
  rt x3=.9w;y3=.21h;
  pickup pencircle xscaled 1.3wid yscaled .5wid rotated 20;
  draw z2{dir 45}..z3.. {dir 220}z0;
endchar;enddef;
%
%
def do_mod_flat =
beginchar (24, .7em#, 1.0cap#, 0);"a flat char";
  pickup pencircle xscaled .9 wid yscaled .3wid rotated 50;
  lft x0=lft x1=0;
  y0=0h;y1=1.5h;
  draw z0..z1;
  lft x2=x0;y2=.6h;
  rt x3=.9w;y3=.31h;
  pickup pencircle xscaled 1.3wid yscaled .5wid rotated 20;
  draw z2{dir 45}..z3..tension 1.1.. {dir 250}z0;
endchar;enddef;
%
def do_natural =
beginchar (25, .6em#, 1.7cap#, 0);"a natural";
  pickup pencircle xscaled .7 wid yscaled 1.9wid;
 
  lft x0=0;top y0=h;
  x1=x0;y1=.66h;
  x2=x0;y2=.33h;
 
  rt x5=w;y5=y1+.05h;
  x6=x5;y6=y2+.05h;
  x7=x5;bot y7=0;
  draw z0..z2;
  draw z5..z7;
  draw z1..z5;draw z2..z6;
  

endchar;enddef;
%
def do_f_flat =
beginchar (">", .75em#, 1.0cap#, 0);"a wierd flat char";
  pickup pencircle xscaled 1.1wid yscaled .3wid rotated 30;
  x0=x1=.2w;
  y0=-.1h;y1=2.2h;
  x6=-0.8w;y6=y1;
  fill z1..tension 1.2..z6..tension 1.2..cycle;
  draw z0..z1;
  x2=x0;y2=.9h;
  x3=.9w;y3=.5h;
  x4=x2+.1w;y4=y2+ .1h;
  draw z2..tension 16..z4..z3.. {dir 220}z0..tension 56..cycle;
  x7=x0; y7=y2+.5h;
  x8=.7w; y8=y2+.3h;
  draw z2..tension 16..z4..z8..{dir 220}z7;
endchar;enddef;
%
def do_relish_b=
beginchar ("+", em#, 1.0cap#, 0);"a cross relish";
  pickup lute_pen;
  x1=x2=.5w;
  x3=.2w;x4=.8w;
  y1=.2h;y2=.8h;
  y3=y4=.5h;
  draw z1..z2;draw z3..z4;
endchar;enddef;
%
def do_X =
beginchar ("x", .9em#, 1.0cap#, 0);"an X";
  pickup lute_pen;
  x0=x3=0.2w;x2=x1=.8w;
  y0=y2=0.1h;y1=y3=0.6h;
  draw z0..z1;draw z2..z3;
endchar;enddef;
%
def do_it_x =
beginchar (88, em#, 1.0cap#, 0);"a bigger X";
  pickup pencircle xscaled 1.17 wid yscaled .1wid;
  x0=0.2w;x2=.8w;
  x1=x2-.01w;
  x3=x0+.01w;
  y0=y2=0.02h;top y1=top y3=.91h;
  draw z0..z1;draw z2..z3;
  pickup pencircle xscaled .15 wid yscaled .2 wid;
  if unknown ksize: ksize = .73wid;fi;
  if unknown kksize: kksize = .70wid;fi;
  x10=x0 - kksize;
  x11=x0 + ksize;
  y10=y11=y0;
  draw z10..z11;
  x12=x2 - ksize;
  x13=x2 + kksize;
  y12=y13=y2;
  draw z12..z13;
  x14=x3 - kksize;
  x15=x3 + ksize;
  y14=y15=y3;
  draw z14..z15;
  x16=x1 - kksize;
  x17=x1 + ksize;
  y16=y17=y1;
  draw z16..z17;
endchar;enddef;
%
def do_comma=
beginchar ("'", .8em#, 1.0cap#, 0);"a comma relish";
%  pickup pencircle scaled 2.0 wid;
%  x1=.5w;
%  y1=.5h;
%  draw z1;
%  pickup lute_pen;
%  x3=.52w;y3=.545h;
%  x2=.6w;y2=.2h;
%  draw z3{dir 330}..z2;
  pickup pencircle scaled 2.70 wid;
  x1=.5w;
  y1=.5h;
  draw z1;
  pickup bigger_lute_pen;
  x3=.55w;y3=.61h;
  x2=.6w;y2=-.05h;
  draw z3{dir 330}..z2;
endchar;enddef;
%
def do_waves =
beginchar (55, 0.15 in#, 1.0cap#, 0);"a cadenza";
  pickup pencircle scaled .8 wid;
  x0=0w;y0=0.5h;
  x1=.25w;y1=h;
  x2=.5w;y2=y0;
  x3=.75w;y3=0h;
  x4=w;y4=y0;
  draw z0{dir 80}..tension 3..z1..tension 3..z2..tension 3..z3..tension 3..{dir 80}z4;
endchar;enddef;
%
def do_tilde =
beginchar ("~", em#, 1.0cap#, 0);"a tilde";
  pickup pencircle scaled .8 wid;
  x0=-.2w;y0=0.4h;
  x1=.1w;y1=.56h;
  x2=.4w;y2=y0;
  x3=.7w;y3=.24h;
  x4=w;y4=y0;
  draw z0{dir 50}..tension 1.0..z1..tension 1.0..z2
  ..tension 1.0..z3..tension 1.0..{dir 50}z4;
endchar;enddef;
%
def do_slant =
beginchar (1, 2em#, .8cap#, 0);" a slanted line";
   pickup tiny_circle;
   x0=0;y0=0;x1=w;y1=h;
   draw z0..z1;
endchar;enddef;
%
def do_slant_a =
beginchar (2, 2em#, 1.0cap#, 0);" a more slanted line";
   pickup tiny_circle;
   x0=0;y0=0;x1=w;y1=h;
   draw z0..z1;
endchar;enddef;
%
def do_slant_b =
beginchar (3, 1.8em#, 1.0cap#, 0);" a reverse slanted line";
   pickup tiny_circle;
   x0=0;y0=h;x1=w;y1=0;
   draw z0..z1;
endchar;enddef;
%
def do_slant_c =
beginchar (4, 1.8em#, .3cap#, 0);" a slightly slanted line";
   pickup tiny_circle;
   x0=0;y0=0;x1=w;y1=h;
   draw z0..z1;
endchar;enddef;
%
def do_slant_d =
beginchar (5, 2em#, 1.0cap#, 0);" a more slanted line";
   pickup tiny_circle;
   x0=0;y0=0;x1=w;y1=h;
   draw z0..z1;
endchar;enddef;

def do_s_c_ubar =
beginchar ("[", em#, 1.0cap#, 0);" begin a curved underline";
   pickup tiny_circle;
   x0=0w; x1=.4w;x2=w;
   y0=.8h; y1 = .4h; y2=.3h;
   draw z0..z1..z2;
endchar;enddef;
% 
def do_e_c_ubar =
beginchar ("]", em#, 1.0cap#, 0);" end a curved underline";
   pickup tiny_circle;
   x0=0w; x1=.6w;x2=w;
   y0=0.3h; y1 = .4h; y2=0.8h;
   draw z0..z1..z2;
endchar;enddef;
% 
%
def do_slash =
beginchar ("/", em#, 1.0cap#, 0);"a slash";
  pickup pencircle xscaled 1.2wid yscaled .3wid rotated -15;
  x1=0w;x2=w;
  y1=-0.1h;y2=.9h;
  draw z1..z2;
endchar;enddef;
%
def do_two_slash =
beginchar ("s", em#, 1.0cap#, 0);"two slashes";
  pickup pencircle xscaled 1.2wid yscaled .3wid rotated -15;
  x1=0w;x2=w;
  y1=-0.1h;y2=.9h;
  draw z1..z2;
  x3=-.4w;y3=y1+.1h;
  x4=.6w;y4=y2+.1h;
  draw z3..z4;
endchar;enddef;
%
def do_three_slash =
beginchar ("t", em#, 1.0cap#, 0);"three slashes";
  pickup pencircle xscaled 1.2wid yscaled .3wid rotated -15;
  x1=0w;x2=w;
  y1=-0.1h;y2=.9h;
   draw z1..z2;
  x3=-.4w;y3=y1+.1h;
  x4=.6w;y4=y2+.1h;
   draw z3..z4;
  x5=-.8w;y5=y1+.2h;
  x6=.2w;y6=y2+.2h;
   draw z5..z6;
endchar;enddef;
%
def do_minus =
beginchar ("<", .9em#, 1.0cap#, 0); "a minus";
  pickup lute_pen;
  x0=.1w;x1=.9w;
  y0=y1=.4h;
  draw z0..z1;
endchar;enddef;
%
def do_colon =
beginchar ("=", .9em#, 1.0cap#, 0); "a colon";
  my_dot(1, 0.55w, .1h);
  my_dot(2, 0.55w, .5h);
endchar;enddef;
%
def do_hat =
beginchar ("^", .9em#, 1.0cap#, 0); " a hat";
   pickup pencircle xscaled 1.2wid yscaled .6wid rotated 50;
   x0=.2w;x1=.5w;x2=.8w;
   y0=.3h;y1=.6h;y2=.3h;
   draw z0..z1;draw z1..z2;
endchar;enddef;
%
def do_star =
beginchar ("@", .7cap#, 1.0cap#, 0); " a star";
   pickup pencircle scaled .8 wid;
   x0=x2=.5w;
   lft x1=0w;rt x3=w;
   y0=.2h;y1=y3=.5h;y2=.8h;
   x4=x6=.22w;   x5=x7=.78w;
   y4=y7=.3h;   y5=y6=.7h;
   draw z0..z2;draw z1..z3;
   draw z4..z5; draw z6..z7;
endchar;enddef;%
%
%
def do_smile =
beginchar ("_", 1.2em#, 1.0cap#, 0); " a smile";
   pickup pencircle xscaled 1.0 wid yscaled .5 wid;
   x0=0w;x1=.5w;x2=w;
   y0=0h;y1=y0-.4h;y2=y0;
   draw z0..z1..z2;
   endchar;enddef;
%
def a_dot(suffix $)(expr wid)(expr hi) = 
 x$1=wid;x$2=x$4=wid + .14h;x$3=wid +.28h;
 y$1=y$3= hi-.14h;y$2= hi-.28h;y$4= hi;
 fill z$1..z$2..z$3..z$4..cycle;
enddef;
%
def do_arpeggio =
beginchar (56, 1.0em#, 1.0cap#, 0);"a little thing to indicat repeat";
%        pickup lute_pen;
	pickup pencircle xscaled 1.2wid yscaled .3 wid rotated 70;
        x0=0w; y0=0.15h;
        x1=1w; y1=0.65h;
        a_dot(1, .29w, 0.886h);
        a_dot(2, .41w , 0.23h );
        draw z0{dir 45}..tension 2.0..{dir 60}z1;
	endchar;enddef;
%
def do_rpt =
beginchar (57, 2.5em#, .8cap#, 0);"a little thing to indicat repeat";
	pickup lute_pen;
	x0=0w; y0=0h;
	x1=1w;y1=1h;
	a_dot(1,.15w, .9h);
	a_dot(2, .85w - .28h, .1h + .28h);
	draw z0{right}..tension 2.0..{right}z1;
endchar;enddef;
%
%
%		NUMBERS
%
def do_zero_p =
 x0=x1=.5w;
 y0=0.02h;y1=0.92h;
 draw z0..tension 1.7..z1..tension 1.7..cycle;
enddef;
def do_zero =
beginchar (150, .9em#, 1.0cap#, 0);" a 0 bourdon";
 pickup pencircle xscaled 1.0wid yscaled .3wid rotated 5;do_zero_p;
endchar;enddef;
%
def do_zero_f =
beginchar (140, .55em#, .7cap#, 0);" a tiny 0 ";
 pickup pencircle xscaled 0.8wid yscaled .3wid rotated 5;do_zero_p;
endchar;enddef;
%
def do_one_p =
 pickup pencircle xscaled 1.2wid yscaled .3wid rotated 25;
 pickup lute_pen;
 x0=.3w;y0=.7h;
 x1=x2=.5w;
 y1=.95h;y2=.045h;
 x3=.3w; x4=.7w;
 y3=-.05h;y4=.15h;
 x5=x1;y5=y2;
 draw z0{dir 45}..z1;
 draw z1..z2;
 draw z3..z5..z4;
enddef;
%
def do_one_b =
beginchar (151, .9em#, 1.0cap#, 0);" a 1 bourdon";
 pickup pencircle xscaled 1.2wid yscaled .3wid rotated 25;
 do_one_p;
endchar;enddef;
%
def do_one_f =
beginchar (141, .5em#, .7cap#, 0);" a small 1";
 pickup pencircle xscaled .8wid yscaled .3wid rotated 25;
 do_one_p;
endchar;enddef;
%
def do_two_p =
 x1=0.15w;y1=.68h;
 x2=.5w;y2=.9h;
 x3=.8w;y3=.68h;
 x4=0.05w;y4=0h;
 x5=.5w;y5=.1h;
 x6=.95w;y6=0h;
 draw z1..z2..z3..{dir 250}z4;
% pickup pencircle xscaled 1.2wid yscaled .3wid rotated 40;
 draw z4..z5..z6;
enddef;
%
def do_bb =
beginchar (152, .9em#, 1.0cap#, 0);" a 2 bourdon";
 pickup pencircle xscaled 1.2wid yscaled .3wid rotated 10;
 do_two_p;
endchar;enddef;
%
def do_two_f =
beginchar (142, .55em#, .7cap#, 0);" a small 2";
 pickup pencircle xscaled .8wid yscaled .3wid;
 do_two_p;
endchar;enddef;
%
def do_BIG_two =
beginchar ("R", 1.8em#, 2.0cap#, 0);"a big two";
 pickup pencircle xscaled 2.3wid yscaled .9wid;
% beginchar ("R", .9em#, 1.0cap#, 0);"a big two";
% pickup pencircle xscaled 1.15wid yscaled .45wid;
 lft x1=0.05w;y1=.68h;
 x2=.5w;top y2=h;
 rt x3=.96w;y3=.72h;
 lft x4=0.0w;
 bot y4=0h;
 x7=x4;y7=y4+.02h;
 x5=.5w;bot y5=.06h;
% rt x6=w;y6=y4;
 x6=1.01w;y6=y7+0.01h;
 draw z1..z2..z3..{dir 250}z4;
% draw z4..z5..z6;
penpos7(1.4wid,90);
penpos5(1.7wid,90);
penpos6(2.1wid,66);
penstroke  z7e..z5e..z6e;
endchar;enddef;
%
def do_three_p =
 x1=.15w; x4=.1w;
 y1=y2=.66h;
 y3=y4=.28h;
 x2=.79w;
 x3=.85w;
 x5=.51w;y5=.52h;
 draw z1{dir 100}..z2..z5;
 draw z5..z3..{dir 80}z4;
enddef;
def do_bbb =
beginchar (153, .8em#, 1.0cap#, 0);" a 3 bourdon";
 pickup pencircle xscaled 1.0wid yscaled .5wid;
 do_three_p
endchar;enddef;
%
def do_three_f =
beginchar (143, .55em#, .7cap#, 0);" a small 3";
 pickup pencircle xscaled .8wid yscaled .3wid;
 do_three_p;
endchar;enddef;
%
def do_BIG_three =
beginchar ("T", 1.8em#, 2.0cap#, 0);"a big three";
 pickup pencircle xscaled 2.3wid yscaled .8wid;

 lft x1=0.07w; lft x4=0;
 y1=y2=.76h;
 y3=y4=.28h;
 x2=.79w;
 x3=.85w;
 x5=.51w;y5=.54h;
 x11=.5[x1,x2];top y11=h;
 x34 = .5[x3,x4];bot y34=0;
 draw z1{dir 100}..z11..z2..z5 &
 z5..z3..z34..{dir 80}z4;

def do_bbb =
beginchar (153, .8em#, 1.0cap#, 0);" a 3 bourdon";
 pickup pencircle xscaled 1.0wid yscaled .5wid;
 do_three_p
endchar;enddef;
%

endchar;enddef;
%
def do_four_p =
 x1=x2=x3=.65w; lft x4=0w; x5=w; top y1=top y3=h; y2=0h; y4=y5=.27h;
 draw z1..z2; draw z3{dir 250}..z4; draw z4..z5;
enddef;
%
def do_bbbb =
beginchar (154, .8em#, 1.0cap#, 0);" a 4 bourdon";
 pickup lute_pen;  do_four_p;
endchar;enddef;
%
def do_four_f =
beginchar (144, .55em#, .7cap#, 0);" a small 4";
 pickup pencircle xscaled .6wid yscaled .3wid; do_four_p;
endchar;enddef;
%
def do_BIG_four =
beginchar ("S", 2.0em#, 2.0cap#, 0);"a big four";
 pickup pencircle xscaled 2.5wid yscaled .8wid rotated 45;
  do_four_p;
endchar;enddef;
%
def do_five_p =
 x1=x3=0.05w; x2=.9w; x7=.5w;y7=.95h; x4=x5=.7w;
 x6=0w; y1=y2=h; y3=y4=7/13h; y5=y6=.05h;
 draw z1..z7..z2; draw z1..z3; draw z3..z4..z5..z6;
enddef;
%
def do_bbbbb =
beginchar (155, .8em#, .9cap#, 0);" a 5 bourdon";
 pickup lute_pen;do_five_p;
endchar;enddef;
%
def do_five_f =
beginchar (145, .55em#, .7cap#, 0);" a small 5";
 pickup pencircle xscaled .8wid yscaled .3wid; do_five_p;
endchar;enddef;
%
def do_BIG_five =
beginchar ("V", 1.20em#, 2.0cap#, 0);" a big 5 here it is!";
 pickup pencircle xscaled 2.0wid yscaled .9wid rotated 75;
% do_five_p;
 lft x1=.22w;
 rt x2=w; 
 top y1= top y2=h;
 x7=.65[x1,x2]; top y7=.99h;
 x3=0.13w; y3=.70h;
 draw z2{dir 210}..tension 1.5..z7..{dir 190}z1 &  z1..z3; 
 y4=6/13h;
 x4=.77w;
 x5=.67w; y5=.17h;
 lft x6=0;
  y6=0; 
 penpos3(2.0wid, 75); penpos4(2.0wid,30); penpos5(2.0wid,0);penpos6(.8wid,330);
 penstroke z3e..z4e..z5e..z6e;
endchar;enddef;
%
def do_six_p =
 x4=.85w;y4=.9h;
 x1=.5w;y1=h;
 x2=x1;y2=0h;
 x3=x1;y3=7/13h;
 x5=.1w;y5=.3h;
 draw z4..tension 1.2..z1..tension 1.5..z2..tension 1.1..z3..z5;
enddef;
%
def do_BIG_six =
beginchar ("P", 1.40em#, 2.0cap#, 0);" a big 6 here it is!";
pickup pencircle xscaled 2.0wid yscaled .9wid rotated 5;
% beginchar ("P", .7em#, 1.0cap#, 0);" a big 6 here it is!";
% pickup pencircle xscaled 1.0wid yscaled .45wid rotated 5;
 rt x4=1.05w;top y4=1.02h;
 x1=.5w; y1=.83 h;
 lft x6=0w;y6=.5h;
 x2=x1;bot y2=0h;
 x3=x1;y3=.546h;
 rt x8=w; y8=.5[y2,y3];
 x5=.09w;y5=.3h;
 draw z4..tension 1.1..z1..tension 1.0..
   z6..z2..z8..z3..z5;
endchar;enddef;
%
def do_bbbbbb =
beginchar (156, 1.0em#, 1.0cap#, 0);" a 6 bourdon";
 pickup lute_pen;do_six_p;
endchar;enddef;
%
def do_six_f =
beginchar (146, .55em#, .7cap#, 0);" a small 6";
 pickup pencircle xscaled .8wid yscaled .3wid; do_six_p;
endchar;enddef;
%
def do_seven_p =
 x1=0w;y1=h;
 x2=.5w;y2=.95h;
 x3=w;y3=h;
 x5=.5w;y5=-.1h;
 draw z1..z2..z3;
 draw z3..{dir 270}z5;
enddef;
%
def do_bbbbbbb =
beginchar (157, .8em#, .9cap#, 0);" a 7 bourdon"; 
pickup lute_pen;do_seven_p;
endchar;enddef;
%
def do_seven_f =
beginchar (147, .55em#, .57cap#, 0);" a small 7";
 pickup pencircle xscaled .8wid yscaled .3wid; do_seven_p;
endchar;enddef;
%
def do_eight_p =
 x1=0.5w;y1=h;
 x2=x1;y2=7/13h;
 x3=x4=x1;
 y3=y2;y4=0h;
 draw z1..z2..cycle;
 draw z3..z4..cycle;
enddef;
def do_bbbbbbbb =
beginchar (158, .8em#, 1.0cap#, 0);" a 8 bourdon";
 pickup lute_pen;do_eight_p;
endchar;enddef;
%
def do_eight_f =
beginchar (148, .55em#, .7cap#, 0);" a small 8";
 pickup pencircle xscaled .8wid yscaled .3wid; do_eight_p;
endchar;enddef;
%
def do_octave =
beginchar  (26, .42em#, .62cap#,0);"a tiny 8 for octaves";
    pickup pencircle xscaled .73wid yscaled .14wid; do_eight_p;
endchar;enddef;
%
def do_nine_p =
 x4=.1w;y4=.1h;
 x1=.2w;y1=-0.070h;
 x2=.4w;y2=1h;
 x3=x2;y3=.5h;
 x5=.9w;y5=.7h;
 draw z5..z3..tension 1.2..z2..z5..tension 1.0..z1;
enddef;
%
def do_bbbbbbbbb =
beginchar (159, .8em#, 1.0cap#, 0);" a 9 bourdon";
 pickup pencircle xscaled 1.2 wid yscaled .3wid rotated 30;
 do_nine_p;
endchar;enddef;
%
def do_nine_f =
beginchar (149, .55em#, .7cap#, 0);" a small 9";
 pickup pencircle xscaled .8wid yscaled .3wid; do_nine_p;
endchar;enddef;
%
def do_i_u =
beginchar (248, .8em#, 1.0cap#, 0); " a little u thingy";
 pickup pencircle xscaled .8wid yscaled .5wid rotated 30;
 x0=0;x1=.5w;x2=w;
 x3=.2w;x4=.8w;
 y0=y2=.3h;y1=.6h;
 y3=y4=.05h;
 draw z3..z0..z1..z2..z4;
endchar;enddef;
%
def do_i_w =
beginchar (249, .9em#, 1.0cap#, 0); " a little u thingy";
 pickup pencircle xscaled .8wid yscaled .5wid rotated 3;
  lft x0=0;y0=y6=.53h;
  rt x6=w;
  x3=.5w; bot y3=0;
  x8=x9=x3;
  y8=y0+.1h;
  y9=-.07h;
  draw z0..z3..z6;draw z8..z9;
% x0=.1w;x1=0w;x2=.25w;x3=.42w;x4=.75w;x5=w;x6=.9w;x7=.58w;
% y0=y3=y6=y7=.6h;
% y1=y5=.45h;
% y2=y4=0h;
% draw z0..z1..z2..z3;draw z7..z4..z5..z6;
endchar;enddef;
%
def do_test =
beginchar (250, em#, 1.0cap#, 0); " a test";
 pickup tiny_circle;
 x0=0; y0=0;
 x1=0; y1=h;
 x2=h; y2=h;
 x3=h; y3=0;
 draw z0..z2;
 draw z0..z1;draw z1..z2;draw z2..z3;draw z3..z0;
endchar;enddef;
%
%   	fonts from Mace
%
%
def mace_a =
beginchar (160, 0, 1.0cap#, 0); " mace a ";
endchar;enddef;
%
def mace_b =
beginchar (161, 0, 1.0cap#, 0); " mace b";
endchar;enddef;
%
def mace_c =
beginchar (162, 0, 1.0cap#, 0); "mace c";
endchar;enddef;
%
def mace_d =
beginchar (163, 0, 1.0cap#, 0); "mace d";
endchar;enddef;
%
def mace_e =
beginchar (164, em#, 1.0cap#, 0); "mace e";
 pickup lute_pen;
 x1=.2w;y1=.25h;
 x2=.5w;y2=.7h;
 rt x4=1.0w;y4=.3h;
 draw z1..tension 2.5..z2..tension 1.2..z1..tension .8..{dir 55}z4;
endchar;enddef;
%
def mace_f =
beginchar (165, em#, 1.0cap#, 0); "mace f";
  pickup lute_pen;
  rt x0=w;y0=.7h;
  x1=.75w;top y1=.95h;
  x2=.5w;bot y2=y0;
  x3=x2;y3=.3h;
  x4=.25w;y4=0h;
  lft x5=0w;y5=y3;
  x6=.25w;x7=.75w;y6=y7=mid_bar;
  draw z0..z1..z2..z3..z4..z5;
	draw z6..z7;
endchar;enddef;
%
def mace_g =
beginchar (166, 0, 1.0cap#, 0); "mace g";
endchar;enddef;
%
def mace_h =
beginchar (167, 0, 1.0cap#, 0); "mace h";
endchar;enddef;
%
def mace_i =
beginchar (168, 0, 1.0cap#, 0); "mace i";
endchar;enddef;
%
def mace_j =
beginchar (169, 0, 1.0cap#, 0); "mace j";
endchar;enddef;
%
def mace_k =
beginchar (170, 0, 1.0cap#, 0); "mace k";
endchar;enddef;
%
def mace_l =
beginchar (171, 0, 1.0cap#, 0); "mace l";
endchar;enddef;
%
def mace_m =
beginchar (172, 0, 1.0cap#, 0); "mace m";
endchar;enddef;
%
def mace_n =
beginchar (173, 0, 1.0cap#, 0); "mace n";
 pickup tiny_circle;
endchar;enddef;
%
% from robinson's schoole
%
%
def rob_a =
beginchar (175, 0, 0, 0); "robinson a";
 pickup tiny_circle;
endchar;enddef;
%
def rob_b =
beginchar (176, .8em#, 1.0cap#, 0); "robinson n";
 pickup pencircle xscaled 1.5wid yscaled .5 wid rotated 50;
 lft x1=0w;top y1=h;
 x2=x1;y2=.4h;
 x3=x2; y3=0.07h;
 draw z1..z2..z3;   % stem
 pickup pencircle xscaled 1.1wid yscaled .3 wid rotated 10;
 x4=x2;y4=y2;
 rt x6=w;y6=.25h;
 x7=.5w;bot y7=-0.010h;
 x8=x2;y8=.19h;
 draw z4..z6..z7..z8;
endchar;enddef;
%
def rob_c =
beginchar (177, .8em#, 1.0cap#, 0); "robinson c";
 pickup lute_pen;
 x1=.9w;y1=.536h;
 x4=.6w;y4=.576h;
 x2=.129w;y2=.464h;
 x3=.32w;y3=.096h;
 x5=.48w;y5=.0h;
 x6=.62w;y6=.08h;
 x7=0w;y7=.488h;
 x8=.18w;y8=.432h;
 x22=.22w;y22=.5[y8,y3];
 penpos1(1.1wid, 50);penpos4(1.4wid,50);penpos2(1.5wid,70);
 penpos7(.7wid,40);
 penpos8(1.2wid,40);
 penpos22(1.09wid,40);
 penpos3(.8wid,30);
 penpos5(.7wid,0);
 penpos6(.7wid,10);
 penstroke z1e..tension 2.0..z4e..z2e; % top
 penstroke z7e..z8e..tension 2..z22e..z3e..z5e;
 penstroke z5e{dir 30}..z6e;  % tail
endchar;enddef;
%
def rob_d =
beginchar (178, .85em#, 1.0cap#, 0); "robinson n";
 pickup pencircle xscaled 1.2wid yscaled .2wid rotated 50;;
 x3=.55w;y3=.6h;
 x4=-.6w;y4=0.90h;
 penpos3(1. wid, 45);penpos4(2.0wid, 45);
 penstroke z3e..z4e;

 x1=.8w;y1=.45h;
 x2=.5w;bot y2=-0.030;
 x5=.1w;y5=y1;
 penpos1(1.2wid,20);
 penpos2(0.8wid,270);
 penpos5(0.6wid,180);
 penstroke z3e..z1e..z2e..z5e..z3e;
% draw z3..z1..z2..z5..z3;
 
endchar;enddef;
%
def rob_e =
beginchar (179, .84em#, 1.0cap#, 0); "robinson e";
 pickup pencircle xscaled 1.4wid yscaled .6 wid rotated 50;

 x1=.5w;top y1=.8h;
 x2=.35w;y2=.7h;
 x3= 0.2w;y3=.2h;
 x4=.5w;bot y4=0h;
 rt x5=w;y5=.3h;
  draw z1{dir 200}..z2..tension 1.2..z3..z4..{dir 45}z5;
 x6=.7w;y6=.58h;
 x7=0.6[x2,x3];y7=0.6[y2,y3];
  draw z6{dir 250}..{dir 180}z7;
endchar;enddef;
%
def rob_f =
beginchar (180, .85em#, 1.0cap#, 0); "robinson f";
  pickup lute_pen;
  x5=.5w;y5=.6h;
  penpos5(.4wid, 170);
  rt x0=.9w;y0=.7h;
  penpos0(.6wid, 170);
  x1=.75w;top y1=h;
  penpos1(.6wid, 230);
  x2=.5w;bot y2=y0;
  penpos2(1.5wid, 40);
  x3=x2;y3=.3h;
  penpos3(1.5wid, 40);
  x4=.5w;y4=0h;
  penpos4(1.5wid, 40);
  x6=.1w;x7=.9w;y6=y7=.45h;
%  draw z5..tension 3.0..z0..z1..z2..z3..z4;
  penstroke z5e..tension 2.0..z0e..z1e..z2e..z3e..tension 1.0..z4e;
  draw z6..z7;
endchar;enddef;
%
def rob_g =
beginchar (181, em#, 1.0cap#, 0); "robinson g";
 pickup bigger_lute_pen;
 rt x1= w;top y1=h;
 x2=x1;y2=.3h;
 x3=.5w;bot y3=0h;
 x4=0w;y4=.15h;
 draw z1{dir 260}..tension 1.8..z2..z3..z4;
 x6=.3w;y6=.8h;
 x7=.6w;y7=.55h;
 x8=x1; y8=.9y1;
 draw z1..tension 1.5..z6..z7..tension 1.5..z8;
endchar;enddef;
%
def rob_h =
beginchar (182, .85em#, 1.0cap#, 0); "robinson h";
 pickup bigger_lute_pen;
 x1=.18w;bot y1=0.0h;
 x5=.22w;y5 =.14h;
 x2=.20w; y2=.53h;
 x3=.77w; top y3=.8h;
 x4= 0w;y4=.35h;
 draw z1..z5..tension 1.2..z2..tension 1.03..z3..tension 2..z4;
 x7= .9w; y7=.28h;
 x8= .83w;bot y8 = -.050h;
 draw z1..z5..z7..tension 1.4..z8;
endchar;enddef;
%
def rob_i =
beginchar (183, 0, 1.0cap#, 0); "robinson i";
% pickup lute_pen;
 pickup pencircle xscaled 1.4 wid yscaled .4 wid rotated 30;;
 x2=.35w;
 x1=x3=x4=.5w;
 y1=.50h;y2=-.05h;
 x5=x2; y5=y1;
 x6=.8w;y6=.07h;
 y3=.85h;y4=.75h;
 draw z5..tension 2..z1..tension 4..z2..tension 3..{dir 100}z6;
 draw z3..z4;
endchar;enddef;
%
def rob_j =
beginchar (184, 0, 1.0cap#, 0); "robinson j";
 pickup tiny_circle;
endchar;enddef;
%
def rob_k =
beginchar (185, .8 em#, 1.0cap#, 0); "robinson k";
 pickup bigger_lute_pen;
 x1=.18w;bot y1=0.0h;
 x5=.22w;y5 =.14h;
 x2= .18w; y2=.75h;
 x3= .57w; top y3=.94h;
 x4= 0w;y4=.52h;
 draw z1..z5..tension 1.3..z2..z3..tension 1.7..z4;
 x44=.24w;y44=.42h;
 x6= .5w;y6=.57h;
 x7= .47w;y7=.34h;
 x8= x44;y8=y44;
 draw z44..tension 2.8..z6..z7..z8;
	pickup pencircle xscaled 1.2wid yscaled .3wid rotated 30;
 x12=x8;y12=y8;
 x9= .5[x8, x10]; y9=.16h;
 rt x10=w; y10=0h;
 penpos12(.6wid,20);penpos9(.8wid,20);penpos10(1wid,20);
 penstroke z12e..z9e..z10e;
endchar;enddef;
%
def rob_l =
beginchar (186, .5em#, 1.0cap#, 0); "robinson l";
 pickup pencircle xscaled 1.4wid yscaled .3 wid rotated 50;
 x1=.36w;bot y1=0.0h;
 x5=.42w;y5 =.14h;
 x2= .36w; y2=.75h;
 rt x3= w; top y3=.94h;
 x4= 0w;y4=.4h;
 x6= .97w;y6=.24h;
 draw z1..z5..tension 1.3..z2..z3..tension 1.7..z4;
 draw z1{dir  30}..z6;
endchar;enddef;
%
def rob_m =
beginchar (187, 0, 1.0cap#, 0); "robinson n";
 pickup tiny_circle;
endchar;enddef;
%
def rob_n =
beginchar (188, 0, 1.0cap#, 0); "robinson n";
 pickup tiny_circle;
endchar;enddef;
%
% some fancy repeats
%
def rep_one =
beginchar (254, 2.4em#, 2.2cap#, 0); "a repeat";
 pickup pencircle xscaled .5wid yscaled .1wid;
 x11=.576w; y11=.88h;
 penpos10(.3wid,180); penpos11(.3wid,0);
 x1=.45w;y1=.97h;
 penpos1(.4wid,90);
 x2=0.08w;y2=.75h;
 penpos2(1.5wid,180);
 x3=x1;y3=.5h;
 penpos3(1.9wid,270);
 x4=.92w;y4=.25h;
 penpos4(1.6wid,180);
 x5=.55w;y5=0.03h;
 penpos5(.6wid,90);
 x12=.424w; y12=.12h;
 penpos12(.4wid,0);
 penstroke z11e..z1e..z2e..tension 1.0..z3e..tension 1.1..z4e..z5e..z12e;
 x20=0w;y20=0h;
 x21=w;y21=h;
 draw z20..z21;
 % dots
 x22=.4w;y22=.16h;
 x23=x22+.2w;y23=y22;
 pickup pencircle scaled .1wid;
 fill z22..z23..cycle;
 x24=.6w;y24=.84h;
 x25=x24-.2w;y25=y24;
 fill z24..z25..cycle;
 % side dots
 x26=.84w;y26=.58h;
 x27=x26+.16w;y27=y26;
 fill z26..z27..cycle;
 x28=.16w;y28=.42h;
 x29=x28-.16w;y29=y28;
 fill z28..z29..cycle;
endchar;enddef;
%
def rep_two =
%  beginchar (255, 1.8em#, 2.0cap#, 0); "a repeat";
  beginchar (255, 1.35em#, 1.5cap#, 0); "a repeat";
 pickup pencircle xscaled 1.2wid yscaled .4wid rotated 30;
 x1=.5w;top y1=h;
 x2=.5w;y2=.75h;
 x3=.6w;y3=.2h;
 x4=.33w;y4=0.03h;
 x5=.08w;y5=.28h;
 x6=.5w;y6=.32h;
 x7=w;y7=.15h;
 draw z1..z2..z3..z4..z5..z6..z7;
 x26=0w;y26=.87h;
 x27=x26+.13w;y27=y26;
 fill z26..z27..cycle;
 x28=w;y28=y26;
 x29=x28-.13w;y29=.9h;
 fill z28..z29..cycle;
endchar;enddef;
%
%
def mb_one =
beginchar (237, 1.35em#, 2.1cap#, 0 ); "board single flag";
 do_v_bar;
 pickup pencircle xscaled 1.9 wid yscaled .3 rotated 70;
 x1=v_bar;x2=w;
 top y1=h;y2=y1 - 19/30h;
 x25=1/2[x1,x2];
 y25=1/2[y1,y2];
 draw z1{dir 250}..tension 1.4..z25..{dir 250}z2;
endchar;enddef;
%
def mb_two =
beginchar (238, 1.2em#, 2.1cap#, 0 ); "board double flag";
   pickup lute_pen;x3 = x4 = v_bar;top y3 = h;top y4 = 0;
   draw z3..z4;
   pickup pencircle xscaled 1.7 wid yscaled .3 rotated 70;
   x5=v_bar; top y5=.95h;
   x6=.7w;   y6=y5-.19h;
   x7= x6-.1w;   y7=y6-.19h;
   x8= w;  y8=y7-.13h;

draw z5{dir 320}..tension 1.4..z6..z7..tension 1.4..{dir 160}z8;
endchar;enddef;
%
def mb_three =
beginchar (239, 1.1em#, 2.1cap#, 0 ); "board triple flag";
   pickup lute_pen;x3 = x4 = v_bar;top y3 = h;top y4 = 0;
   draw z3..z4;
   pickup pencircle xscaled 1.7 wid yscaled .3 rotated 70;
   x5=v_bar; top y5=.95h;
   x6=.59w;y6=y5 -.15h;
   x7= x6-.13w;y7=y6-.11h;
   x8= .8w;y8=y7-.1h;
   x9= x8-.11w;y9=y8-.12h;
   x10=w;y10=y9-.1h;
draw z5{dir 330}..tension 1.4..z6..z7..tension 1.4..z8..z9..tension 1.4..{dir 170}z10;
endchar;enddef;
%
def mb_four =
beginchar (240, 1.2em#, 2.1cap#, 0 ); "board quad flag";
   pickup lute_pen;x3 = x4 = v_bar;top y3 = h;top y4 = 0;
   draw z3..z4;
   pickup pencircle xscaled 1.7 wid yscaled .3 rotated 60;
   x5=v_bar; top y5=.95h;
   x6=.54w;    y6=y5-.15h;
   x7= x6-.15w;   y7=y6-.12h;
   x8=x7+.3w;  y8=y7-.11h;
   x9= x8-.15w;   y9=y8-.12h;
   x10=x9+.3w; y10=y9-.11h;
   x11= x10-.15w; y11=y10-.12h;
   x12=x11+.3w;   y12=y11-.11h;
   draw z5{dir 330}..tension 1.4..z6..z7..tension 1.4..
   z8..z9..tension 1.4..z10..z11..tension 1.4..z12;
endchar;enddef;
%
def mb_five =
beginchar (241, 1.2em#, 2.1cap#, 0 ); "board quint flag";
   pickup lute_pen;x3 = x4 = v_bar;top y3 = h;top y4 = 0;
   draw z3..z4;
   pickup pencircle xscaled 1.7 wid yscaled .3 rotated 60;
   x5=v_bar; top y5=.95h;
   x6=.54w;    y6=y5-.15h;
   x7= x6-.15w;   y7=y6-.12h;
   x8=x7+.3w;  y8=y7-.11h;
   x9= x8-.15w;   y9=y8-.12h;
   x10=x9+.3w; y10=y9-.11h;
   x11= x10-.15w; y11=y10-.12h;
   x12=x11+.3w;   y12=y11-.11h;
   x13= x12-.15w; y13=y12-.12h;
   x14=x13+.3w;   y14=y13-.11h;
   draw z5{dir 330}..tension 1.4..z6..z7..tension 1.4..
   z8..z9..tension 1.4..z10..z11..tension 1.4..z12..z13..tension 1.4..z14;
endchar;enddef;
%
def do_b_tie =
beginchar (128, 1.5em#, .6cap#, 0); "large tie for tab and music";
	pickup pencircle xscaled 1.2wid yscaled .8wid;
	x0=0;x1=.5w;x2=w;
	y0=.0h;y1=h;y2=y0;
	draw z0..z1..z2;
endchar;
enddef;
%
def do_s_tie =
beginchar (129, .6em#, 1.0cap#, 0); "small tie for spanish text";
	pickup pencircle scaled 0.6 wid;
	x0=0;x1=.5w;x2=w;
	y0=.64h;y1=.81h;y2=y0;
	draw z0..z1..z2;
endchar;
enddef;
%
def do_in_zero =
beginchar (130, .9em#, .9cap#, 0); "italian zero";
	pickup pencircle xscaled 1.2wid yscaled .8wid;
	x0=x2=.5w;lft x1=0;rt x3=w;
	top y0=h;bot y2=0;y1=y3=.5h;
	draw z0..z1..z2..z3..cycle;
endchar;enddef;
%
def do_in_one =
beginchar (131, .4em#, .9cap#, 0); "italian one";
pickup pencircle xscaled 1. wid yscaled .2wid;
x0=.5w;top y0=h;
x1=x0;bot y1=0;
draw z0..z1;
pickup pencircle xscaled .1wid yscaled .8wid;
lft x2= lft x4 = 0;rt x3= rt x5 = w;top y2=top y3=h;
bot y4=bot y5=0;
draw z2..z3;draw z4..z5;
endchar;enddef;
%
def do_in_two =
beginchar (132, .7em#, .9cap#, 0); "italian two";
 pickup pencircle xscaled 1.2 wid yscaled 1 rotated 30;
 x1=0.09w; y1=.82h;
 x2=.4w;top y2=h-.2wid;
 rt x3=.92w; y3=.76h;
 x4=0.02w;y4=0.4wid;
 x9=.25[x3,x4];y9=.5[y3,y4];
	penpos1(.9wid, 100);
	penpos2(1.wid, 90);
	penpos3(1.4wid, 30);
	penpos9(1.2wid, -45);
	penpos4(1.1wid, -100);
 penstroke z1e..z2e..z3e..z9e..z4e;
 	x5=0.03w;y5=y4;
 	x7=w;y7=y5+.3wid;
	penpos5(1.1wid,80);
	penpos7(1.1wid,80);
 penstroke z5e{dir 20}..{dir 10}z7e;

endchar;enddef;
%
def do_in_three =
beginchar (133, .7em#, .9cap#, 0); "italian three";
	pickup pencircle xscaled .8wid yscaled 1.1wid;
%	x0=.16w;top y0 = .99h;
	x0=-.03w;top y0 = .99h;
	rt x1=1.003w;top y1= 1.03h;
	x2=.44w;y2=.67h;
	x3=.83w;y3=.3h;
	x4=-.02w;bot y4 = -0.13h;
	draw z0{dir 15}..{dir 18}z1 & z1..z2;
	penpos2(1.2 wid, 90); penpos3(1.38wid, 0); penpos4(1wid,315);
	penstroke z2e..tension 1.03..z3e
	..tension 1.1..{dir 194}z4e;
endchar;enddef;
%
def do_in_four =
beginchar (134, .9em#, .9cap#, 0); "italian four";
	pickup pencircle xscaled 1.4wid yscaled .4wid rotated 40;
	x1=x2=x3=.65w; lft x4=0w; lft x5=w; top y1=top y3=h; top y2=0h; y4=y5=.26h;
	draw z2--z3{(0.1w,0) -z3}...z4--z5;
	pickup pencircle xscaled 2.20wid yscaled 1;
	lft x10=lft x11=1.0w;
	y10=h;y11=0;
	erase draw z10..z11;
	x12=x13=x1;y12=-0.03h;y13=-.4h;
	erase draw z12..z13;
endchar;enddef;
%
def do_in_five =
beginchar (135, 0.75em#, .9cap#, 0); "italian five";
 pickup pencircle xscaled 1.2wid yscaled .5wid rotated 75;
 lft x1=.33w;
 rt x2=1.14w; 
 top y1= top y2=h;
 x7=.65[x1,x2]; top y7=h;
 x3=0.25w; y3=.74h;
 draw z2{dir 200}..tension 1.3..z7..{dir 190}z1 &  z1..z3; 
 y4=6/13h;
 x4=.71w;
 x5=.57w; y5=.14h;
 lft x6=0;
 top y6=-0.03; 
 penpos3(1.1wid, 75); penpos4(1.5wid,30); penpos5(1.65wid,0);penpos6(.7wid,330);
 penstroke z3e{dir 340 }..z4e..z5e..z6e;
endchar;enddef;
%
def do_in_six =
beginchar (136, 0.77em#, .9cap#, 0); "italian six";
 pickup pencircle xscaled 1.2wid yscaled .8wid;
	x1=w;top y1=h;
	x5=.5w;y5=.63h;
	x3=x5;bot y3=0;
	lft x2 = 0;y2=.5[y5,y3];
	rt x4=w;y4=y2;
	draw z1..z2..z3..z4..z5..z2..z3;
	pickup penrazor xscaled 1.0wid;
        lft x10=lft x11 = .98w;
	y10=h;y11=0.6h;
	erase draw z10..z11;
endchar;enddef;
%
def do_in_seven =
beginchar (137, 0.75em#, .9cap#, 0); "italian seven";
 pickup pencircle xscaled 1.2wid yscaled .5wid rotated 75;
	lft  x1=0w; top y1=.98h;
	rt   x2=w; top y2=h;
	x4=x2 -.01w;y4=y2;
	x3=.3w; top y3=-0.0h;
	draw z1{dir 15}..{dir 10}z2;
	penpos2(.9wid, 120); penpos3(1.2wid, 185);
	penpos4(.9wid, 120);
	penstroke z4e..z3e;
endchar;enddef;
%
def do_in_eight =
beginchar (138, 0.76em#, .9cap#, 0); "italian eight";
pickup pencircle xscaled 1.2 wid yscaled 1.wid;
top y1=h; x1=.5w;
bot y5=0; x5=.5w;
lft x2=.1w;y2=11/15h;
 x3=.5w;y3=4/7 h;
 rt x4 = w; y4=4/15 h;;
path p, pp;
p = z1{left}..z2..z3..z4..{left}z5;
pp = p xscaled (-1) shifted (w,0);;
draw p ; draw pp;
endchar;enddef;
%
def do_in_nine =
beginchar (139, .7em#, .9cap#, 0); "italian nine";
pickup pencircle xscaled 1.2 wid yscaled 1.wid;
	x1=0;y1=0;
	x5=.5w;y5=.4h;
	x3=x5;top y3=h;
	rt x2 = w;y2=.5[y5,y3];
	lft x4=0;y4=y2;
%	x6=x5+.15w;y6=y5+.01h;
	x6=x2;y6=y2;
	draw z1..z2..z3..z4..{right}z5..{up}z6;
%	draw z3..z4..{right}z5..{up}z6;
	pickup penrazor xscaled 1.0wid;
        rt x10=.0w;
	x11=x10;
	y10=.4h;y11=-0.1h;
	erase draw z10..z11;
endchar;enddef;
%

def do_zap =
beginchar (251, 1.2em#, 1.0cap#, 0); "the little thing at the end of the line";
   pickup pencircle xscaled 1.7 wid yscaled .6wid rotated 50;
   x0=rt 0;bot y0=0;
   x1=.15w;y1=.3h;
   x2=.3w;bot y2=0;
   x3=.45w;y3=.3h;
   x4=.6w;bot y4=0;
   x5=lft w;top y5=h;
   draw z0..z1;draw z1..z2;draw z2..z3;draw z3..z4;
	penpos4(1.3 wid,45);penpos5(2.1 wid,30);
   penstroke z4e..z5e;
endchar;enddef;
%
def do_sharp =
beginchar (22, .9em#, 1.8cap#, 0); " a sharp";
	pickup pencircle xscaled 2.2 wid yscaled .6 wid rotated 80;
	lft x20=0;rt x21=w;
	y20=0h;y21=2/24h;
	lft x5=lft x7=0;
	rt  x6 = rt x8 = w;
	y5 = 16/24h - .5[y20,y21]; y6=16/24h + .5 [y20,y21];
	y7 = 8/24 h - .5[y20,y21]; y8=8/24h  + .5 [y20,y21];
	draw z5--z6;draw z7--z8;
	x1=x3=.27w;x2=x4=.73w;
	top y1= h- (.73 - .27)[y20,y21];top y2= h;
	bot y3= 0; bot y4= 0 + (.73 - .27)[y20,y21];
	draw z1..z3;draw z2..z4;
endchar;enddef;
%
def do_old_sharp =
beginchar (23, 0.8cap#, 0.8cap#, 0); " a sharp";
	pickup pencircle scaled .9 wid;
	if unknown space:space=.27h;fi;
	lft x1=0;top y1=h;
	rt x2=w-space;bot y2=0;
	path p; p = z1..z2;
	draw p; draw p shifted(space, 0);
	draw p xscaled -1 shifted (w,0);
	draw p xscaled -1 shifted (w-space,0);
%	x1=0;y1 = h-space;
%	x2=.5h-space;y2=.5w;
%	x3=x1;y3=0+space;
%	path p; p = z1..z2&z2..z3;
%	draw p;draw p rotatedaround((.5w, .5w), 90);
%	draw p rotatedaround((.5w, .5w), 180);
%	draw p rotatedaround((.5w, .5w), 270);
endchar;enddef;
%
def do_half_clef =
beginchar (17, 1.6em#, 1.cap#, 2.4cap#); "old O Clef";
	pickup pencircle scaled 1.2wid;
	x2=.5w;top y2=0h;
	x4=x2;y4=1.h;
	lft x1=0w;y1=.5[y2,y4];
	rt x3=w;y3=y1;
	draw z4..z1..z2..z3..cycle;
	x20=.5w;y20=y1;
	x21=1.2w;y21=y1-h;
	x22=x21;y22=y1+h;
	erase fill z20..z21&z21..z22&z22..z20..cycle;
	x10=.2w;y10=1.25h;
	x11=.59w;y11=y10;
	x12=.5[x10,x11];y12=y10+.12h;
	x13=.42w;y13=.3h;
	x18=.5w;top y18=0h;
	x14=x11;y14=y11;
	x15=.55w;y15=-.68d;
	x16=x15-.014w;y16=-.88d;
	x17=.27w;bot y17=-d;
	penpos10(1.6wid,105);
	penpos12(1.6wid,90);
	penpos11(1.6wid,75);
	penpos14(.8wid, 0);
	penpos4(.8wid,0);
	penpos13(.8wid,0);
	penpos18(1.0wid,0);
	penpos15(1.35wid, 0);
	penpos16(1.4wid, -20);
	penpos17(1.6wid, -60);
	penstroke z10e..z12e..z11e;
	penstroke z14e..z4e..tension 1.13..z18e..tension 1.8..z16e..z17e;

endchar;enddef;
%
%
def do_round_clef =
beginchar (18, 1.6em#, 1.cap#, 2.4cap#); "old O Clef";
	pickup pencircle scaled 1.2wid;
	x2=.5w;top y2=0h;
	x4=x2;y4=1.h;
	lft x1=0w;y1=.5[y2,y4];
	rt x3=w;y3=y1;
	draw z1..z2..z3..z4..cycle;
	x10=.2w;y10=1.25h;
	x11=.59w;y11=y10;
	x12=.5[x10,x11];y12=y10+.12h;
	x13=.42w;y13=.3h;
	x18=.5w;top y18=0h;
	x14=x11;y14=y11;
	x15=.55w;y15=-.68d;
	x16=x15-.014w;y16=-.88d;
	x17=.27w;bot y17=-d;
	penpos10(1.6wid,105);
	penpos12(1.6wid,90);
	penpos11(1.6wid,75);
	penpos14(.8wid, 0);
	penpos4(.8wid,0);
	penpos13(.8wid,0);
	penpos18(1.0wid,0);
	penpos15(1.35wid, 0);
	penpos16(1.4wid, -20);
	penpos17(1.6wid, -60);
	penstroke z10e..z12e..z11e;
	penstroke z14e..z4e..tension 1.13..z18e..tension 1.8..z16e..z17e;

      endchar;enddef;
%
%    def do_round_clef =
%      beginchar (27, 3.1em#, 2.1cap#, 0cap#); "old perfect O";
%	pickup pencircle scaled 1.2wid;
%	x1=x5=0.5w;
%	y1=0;
%	y5=h;
%	draw z1..z5..z1;
%      endchar;enddef;
%
    def do_mod_g_clef =
      beginchar (21, 1.8em#, 3.5cap#, 1.0cap#); "G Clef";
	pickup pencircle scaled 3;
	x0=.5w;y0=h;
	x1=x0;y1=0;
%	draw z0..z1;
	pickup pencircle xscaled 2.5wid yscaled .6wid rotated 100;
%	pickup pencircle scaled 7;
	x2=.45w;y2=2/16h;
	x3=.35w;
	top y4=.39h;
	y3=.5[y2,y4];
	rt x5=w;y5=.2[y2,y4];
	x4=.5[x3,x5];
	x6=.5w;bot y6=0.04;
	lft x7=0;y7=.4[y2,y4];
	x8=.5w;y8=.55h;
	rt x9=.81w;y9= 5/6h;
	x10=.72w;top y10=h;
%	x11=.41w;y11=16/18h;
	x11=.41w;y11=14/18h;
	x12=.8w;y12=-.5d;
	x13=.22w;y13=y12;
	x14=.5w;y14=y12;
	y16=.5[y11,y12];
	x16=.5[x11,x12];
	draw z2..z3..z4..z5..z6..z7..tension 1.2..z8..z9..z10
	& z10..z11---z16;
	pickup pencircle scaled .6wid;
	x15=.5[x13,x14];bot y15 = -d;
	draw  z16---z12..z15..{up}z13..z14{down}..z13;
	fill z13..z14..cycle;
endchar;enddef;
%
def do_g_clef =
beginchar (252, 1.6em#, 2.2cap#, 0); "G Clef";
   lft x0=.5w;top y0=h;
   rt x1=w;top y1 = h;
   x2=x1; y2=.64h;
   lft x3=0;y3=.36h;
   x4=.5w;bot y4=0;
   x5=x2;y5=y3;
   x6=x4;y6=.4h;
   x7=.48w;y7=.3h;

   penpos0(2.4 wid,-50);
   penpos1(2.4 wid,-40);
   penpos2(2.2 wid,-50);
   penpos3(2.2 wid,-10);
   penpos4(.8 wid,90);
   penpos5(1.6 wid, 180);
   penpos6(.4wid, 270);
   penpos7(.4wid, 330);
   penstroke z0e{dir 20}..z1e;
   penstroke z1e..z2e;
   penstroke z2e..z3e..z4e..z5e..z6e..z7e;
endchar;enddef;
%
def do_f_clef =
beginchar ("z", 1.6em#, 2.2cap#, 0); "F Clef";
% beginchar ("z", .8em#, 1.1cap#, 0); "F Clef";
lft x0 = lft x4 = 0;
x3 = .6w;y3=.84h;
x2 = .6w;y2=.52h;
bot y0=0;top y4=h;
penpos4(2. wid,10);
penpos3(2. wid,10);
penpos2(2. wid,10);
penpos0(2. wid,10);
penstroke z4e{dir 0}..z3e..z2e..z0e;
pickup pencircle scaled 1.8 wid;
rt x10=w;y10=.77h;
x11=x10;y11=.57h;
draw z10;
draw z11;
endchar;enddef;
%
%
def do_a_rest =
beginchar (253, .7em#, 1.0cap#, 0); "an old rest"; %375 octal
   pickup pencircle xscaled 1.2 wid yscaled .5wid rotated 10;
   lft x0=0; y0=0;
   x1=x0;y1=.84h;
   rt x3=w;y3=.7h;
   draw z0..tension  4.0..z1..tension 1.6..z3;
endchar;enddef;
%
def do_q_rest =
  beginchar (15, .7em#, 1.8cap#, 0); "an quarter rest";
    pickup pencircle xscaled 2.5 wid yscaled .3 wid rotated 120;
    x0=.5w;top y0=h;
    x12=.75w;y12=.83h;
    rt x1=w;y1=.8h;
    lft x4=0;y4=.5h;
    rt x6=w;y6=.32h;
    lft x10=0;y10=.2h;
    x11=0.3w;bot y11=0;

    draw z0..z12..z1&z1..tension 2.3 ..z4..tension 3..z6&z6{dir 160}..z10..z11;
  endchar;
enddef;
%
def do_e_rest =
  beginchar (16, .7em#, 1.0cap#, 0); "an eighth rest";
    pickup pencircle scaled .03 wid;
    lft x0=0w;bot y0=0h;
    rt x1=w;top y1=0.92h;
    
    x3=.4w;y3=h - .2w;
    rt x4=.2w;top y4=h;
    lft x5=0;y5=y3;
    x6=x4;y6=h-.4w;
    
    lft x7=x6+.03w;bot y7=y6+.03h;
    
    fill z3..z4..z5..z6..cycle; 
    pickup pencircle scaled .4 wid;
    draw z7..z1;
    pickup pencircle xscaled 1.2 wid yscaled .5wid rotated 10;
    draw z1..z0;
  endchar;enddef;
%
def do_leaf =
beginchar (8, 1.0cap#, 1.0cap#, 0); "a leaf";
   pickup pencircle xscaled .3 wid yscaled .3wid rotated 10;

 	x0=.2w;y0=.5h;
	x1=.4w;y1=.1h;
	x2=.75w;y2=.1h;
	x3=.8w;y3=.2h;

	x5=.5w;y5=.5h;
	x6=.52w;y6=y5;
	x7=.8w;y7=.43h;
	x8=.7w;y8=.3h;
	x9=.6w;y9=.13h;
	fill z3{dir 240}..tension 1.2..z9..z8..z7..{dir 250}z6
	..z5{dir 90}..z0..z1..z2..tension 1.8..cycle;
endchar;enddef;
%
%%def do_l_paren =
%%beginchar ("(", .42em#, 1.0cap#, .2cap#); "left parenthesis";
%%	pickup pencircle xscaled 1.4wid yscaled .3 wid rotated 10;
%%	rt x1=w;top y1=h;
%%	x3=x1;bot y3=-d;
%%   	lft x2=0w;y2=.5(y1-y3);
%%	draw z1..z2..z3;
%%endchar;enddef;
%%%
%%def do_r_paren =
%%beginchar (")", .42em#, 1.0cap#, .2cap#); "left parenthesis";
%%	pickup pencircle xscaled 1.4wid yscaled .3 wid rotated 10;
%%	lft x1=0;top y1=h;
%%	x3=x1;bot y3=-d;
%%   	rt x2=w;y2=.5(y1-y3);
%%	draw z1..z2..z3;
%%endchar;enddef;
%
def do_l_paren =
beginchar ("(", .42em#, 1.0cap#, .2cap#); "left parenthesis";
        pickup pencircle xscaled 1.0wid yscaled .8 wid rotated 40;
        rt x1=w;top y1=h;
        x4=x1;bot y4=-d;
        lft x2=0;y2=y1;
        x3=x2;y3=y4;
        draw z1..z2;
        draw z2..z3;
        draw z3..z4;
endchar;enddef;
%
def do_r_paren =
beginchar (")", .42em#, 1.0cap#, .2cap#); "left parenthesis";
        pickup pencircle xscaled 1.0wid yscaled .8 wid rotated 40;
        lft x1=0;top y1=h;
        x4=x1;bot y4=-d;
        rt x2=w;y2=y1;
        x3=x2;y3=y4;
        draw z1..z2;
        draw z2..z3;
        draw z3..z4;
endchar;enddef;
%
%	BUILD EVERYTHING
%
def do_all =
  % do_nothing;
  do_whole;do_half;do_long;do_breve;
% tablature flags
  do_line;do_one;do_two;do_three;do_four;do_five;
% more italian style
  do_ii_line;do_ii_one;do_ii_two;do_ii_three;do_ii_four;do_ii_five;
  do_a;do_b;do_c;do_d;do_D;do_e;do_f;do_g;do_h;
  do_F;do_G;;do_H;do_I;
  do_i;do_j;do_k;do_l;do_m;
  do_n;do_o;do_p;
  do_dot;do_c_dot;do_C;do_cut_C;do_big_C;do_big_cut_C;
  do_thumb;do_two_dot;do_three_dot;do_four_dot;
  do_relish_a;do_relish_b;do_X;do_it_x;do_comma;
  do_waves; do_tilde; do_hat;do_star;do_smile;do_slant;do_slant_a;do_slant_b;
  do_slant_c;do_slant_d;
  do_fermata;do_fermata_dot;do_b_fermata;
  do_slash;do_two_slash;do_three_slash;
  do_tail;do_another_tail;do_yet_another_tail;do_box;do_minus;do_colon;
% huge numbers
   do_BIG_two;do_BIG_three;do_BIG_four;do_BIG_five;do_BIG_six;
% large numbers
  do_zero;do_one_b;do_bb;do_bbb;do_bbbb;do_bbbbb;do_bbbbbb;do_bbbbbbb;
  do_bbbbbbbb;do_bbbbbbbbb;
% small numbers
  do_zero_f;do_one_f;do_two_f;do_three_f;do_four_f;do_five_f;do_six_f;
  do_seven_f;do_eight_f;do_nine_f;
% italian flags
  do_i_one;do_i_two;do_i_three;do_i_four;do_i_five;do_i_six;do_i_seven;
  do_ri_one;do_ri_two;do_ri_three;do_ri_four;do_ri_five;do_ri_six;%%do_i_six;do_i_seven;
  do_i_eight;
  do_iii_two;do_iii_three;do_iii_four;do_iii_five;do_iii_six;do_iii_seven;
% modern notes
  do_sol;do_fflag;do_rflag;do_open;do_mod_open;
  do_solid;do_solid_o;do_solid_oo;do_solid_ooo;do_solid_oooo;do_solid_ooooo;
  do_f_flat; do_flat;
  do_e_c_ubar;do_s_c_ubar;
  do_i_u;do_i_w;
  do_test;
  mace_a;mace_b;mace_c;mace_d;mace_e;mace_f;mace_g;
  mace_h;mace_i;mace_j;mace_k;mace_l;mace_m;mace_n;
  rob_a;rob_b;rob_c;rob_d;rob_e;rob_f;rob_g;
  rob_h;rob_i;rob_j;rob_k;rob_l;rob_m;rob_n;
  rep_one;rep_two;
  mb_one;mb_two;mb_three;mb_four;mb_five;
  do_zap;do_g_clef;do_f_clef;do_a_rest;
  do_mod_g_clef;do_round_clef; do_half_clef;
  do_in_zero;do_in_one;do_in_two;do_in_three;do_in_four;do_in_five;
  do_in_six;do_in_seven;do_in_eight;do_in_nine;
  do_s_tie;do_b_tie;do_rpt;
  do_arpeggio;
  do_sharp;do_old_sharp;do_v;do_vv;
  do_l_paren;  do_r_paren;do_e_rest;do_q_rest;
  do_mod_flat;do_natural;do_octave;
enddef;
%
%
%


